//-----------------------------------------------------------------------------
// Craftsman & Marksman: Life is feudal
//-----------------------------------------------------------------------------

function fillTooltipTemplate()
{
	createCommonTooltipProfiles();

	addTooltipTemplate("tooltipInventory", 666, "createAlchemyTooltip");
	addTooltipTemplate("tooltipInventory", 669, "createAlchemyTooltip");
	addTooltipTemplate("tooltipInventory", 529, "createAlchemyTooltip");
	addTooltipTemplate("tooltipInventory", 457, "createBagTooltip");
	addTooltipTemplate("tooltipInventory", 5, "createCraftToolsEquipmentTooltip");
	addTooltipTemplate("tooltipInventory", 19, "createCraftToolsEquipmentTooltip");
	addTooltipTemplate("tooltipInventory", 2, "createGameplayObjectsQQWTooltip");
	addTooltipTemplate("tooltipInventory", 654, "createGameplayObjectsQQWTooltip");
	addTooltipTemplate("tooltipInventory", 1057, "createGameplayObjectsQuantityWeightTooltip");
	addTooltipTemplate("tooltipInventory", 0, "createNameTooltip");
	addTooltipTemplate("tooltipInventory", 54, "createMeleeWeaponTooltip");
	addTooltipTemplate("tooltipInventory", 55, "createMeleeWeaponTooltip");
	addTooltipTemplate("tooltipInventory", 56, "createMeleeWeaponTooltip");
	addTooltipTemplate("tooltipInventory", 57, "createMeleeWeaponTooltip");
	addTooltipTemplate("tooltipInventory", 58, "createMeleeWeaponTooltip");
	addTooltipTemplate("tooltipInventory", 190, "createMeleeWeaponTooltip");
	addTooltipTemplate("tooltipInventory", 191, "createMeleeWeaponTooltip");
	addTooltipTemplate("tooltipInventory", 192, "createMeleeWeaponTooltip");
	addTooltipTemplate("tooltipInventory", 193, "createMeleeWeaponTooltip");
	addTooltipTemplate("tooltipInventory", 194, "createMeleeWeaponTooltip");
	addTooltipTemplate("tooltipInventory", 195, "createMeleeWeaponTooltip");
	addTooltipTemplate("tooltipInventory", 38, "createMeleeWeaponTooltip");
	addTooltipTemplate("tooltipInventory", 1414, "createMeleeWeaponTooltip");
	addTooltipTemplate("tooltipInventory", 196, "createRangedWeaponTooltip");
	addTooltipTemplate("tooltipInventory", 197, "createRangedWeaponTooltip");
	addTooltipTemplate("tooltipInventory", 607, "createRangedWeaponTooltip");
	addTooltipTemplate("tooltipInventory", 23, "createThrowingWeaponTooltip");
	addTooltipTemplate("tooltipInventory", 654, "createAmmoWeaponTooltip");
	addTooltipTemplate("tooltipInventory", 655, "createAmmoWeaponTooltip");
	addTooltipTemplate("tooltipInventory", 661, "createAmmoWeaponTooltip");
	addTooltipTemplate("tooltipInventory", 1096, "createAmmoWeaponTooltip");
	addTooltipTemplate("tooltipInventory", 29, "createShieldsTooltip");
	addTooltipTemplate("tooltipInventory", 24, "createArmorTooltip");
	addTooltipTemplate("tooltipInventory", 25, "createArmorTooltip");
	addTooltipTemplate("tooltipInventory", 26, "createArmorTooltip");
	addTooltipTemplate("tooltipInventory", 27, "createArmorTooltip");
	addTooltipTemplate("tooltipInventory", 28, "createArmorTooltip");
	addTooltipTemplate("tooltipInventory", 225, "createArmorTooltip");
	addTooltipTemplate("tooltipInventory", 1491, "createOutfitsTooltip");
	addTooltipTemplate("tooltipInventory", 1035, "createDomesticAnimalsTooltip");
	addTooltipTemplate("tooltipInventory", 1036, "createDomesticAnimalsTooltip");
	addTooltipTemplate("tooltipInventory", 1037, "createDomesticAnimalsTooltip");
	addTooltipTemplate("tooltipInventory", 1038, "createDomesticAnimalsTooltip");
	addTooltipTemplate("tooltipInventory", 1039, "createSimpleHorsesTooltip");
	addTooltipTemplate("tooltipInventory", 1040, "createSimpleHorsesTooltip");
	addTooltipTemplate("tooltipInventory", 1685, "createSimpleHorsesTooltip");
	addTooltipTemplate("tooltipInventory", 1041, "createWarhorsesTooltip");
	addTooltipTemplate("tooltipInventory", 1663, "createWarhorsesTooltip");
	addTooltipTemplate("tooltipInventory", 1395, "createBlueprintTooltip");
	addTooltipTemplate("tooltipInventory", 227, "createFoodItemTooltip");
	addTooltipTemplate("tooltipInventory", 228, "createFoodItemTooltip");
	addTooltipTemplate("tooltipInventory", 229, "createFoodItemTooltip");
	addTooltipTemplate("tooltipInventory", 230, "createFoodItemTooltip");
	addTooltipTemplate("tooltipInventory", 231, "createFoodItemTooltip");
	addTooltipTemplate("tooltipInventory", 232, "createFoodItemTooltip");

	addTooltipTemplate("tooltipUsableItems", 0, "createUsableItemsDefaultTooltip");
	addTooltipTemplate("tooltipUsableItems", 54, "createUsableItemsMeleeWeaponTooltip");
	addTooltipTemplate("tooltipUsableItems", 55, "createUsableItemsMeleeWeaponTooltip");
	addTooltipTemplate("tooltipUsableItems", 56, "createUsableItemsMeleeWeaponTooltip");
	addTooltipTemplate("tooltipUsableItems", 57, "createUsableItemsMeleeWeaponTooltip");
	addTooltipTemplate("tooltipUsableItems", 58, "createUsableItemsMeleeWeaponTooltip");
	addTooltipTemplate("tooltipUsableItems", 190, "createUsableItemsMeleeWeaponTooltip");
	addTooltipTemplate("tooltipUsableItems", 191, "createUsableItemsMeleeWeaponTooltip");
	addTooltipTemplate("tooltipUsableItems", 192, "createUsableItemsMeleeWeaponTooltip");
	addTooltipTemplate("tooltipUsableItems", 193, "createUsableItemsMeleeWeaponTooltip");
	addTooltipTemplate("tooltipUsableItems", 194, "createUsableItemsMeleeWeaponTooltip");
	addTooltipTemplate("tooltipUsableItems", 195, "createUsableItemsMeleeWeaponTooltip");
	addTooltipTemplate("tooltipUsableItems", 38, "createUsableItemsMeleeWeaponTooltip");
	addTooltipTemplate("tooltipUsableItems", 1414, "createUsableItemsMeleeWeaponTooltip");
	addTooltipTemplate("tooltipUsableItems", 196, "createUsableItemsRangedWeaponTooltip");
	addTooltipTemplate("tooltipUsableItems", 197, "createUsableItemsRangedWeaponTooltip");
	addTooltipTemplate("tooltipUsableItems", 607, "createUsableItemsRangedWeaponTooltip");
	addTooltipTemplate("tooltipUsableItems", 23, "createUsableItemsRangedWeaponTooltip");
	addTooltipTemplate("tooltipUsableItems", 24, "createUsableItemsArmorTooltip");
	addTooltipTemplate("tooltipUsableItems", 25, "createUsableItemsArmorTooltip");
	addTooltipTemplate("tooltipUsableItems", 26, "createUsableItemsArmorTooltip");
	addTooltipTemplate("tooltipUsableItems", 27, "createUsableItemsArmorTooltip");
	addTooltipTemplate("tooltipUsableItems", 28, "createUsableItemsArmorTooltip");
	addTooltipTemplate("tooltipUsableItems", 225, "createUsableItemsArmorTooltip");
	addTooltipTemplate("tooltipUsableItems", 29, "createUsableItemsShieldsTooltip");

	addTooltipTemplate("tooltipDefault", 0, "createTextTooltip");
	addTooltipTemplate("tooltipEffect", 0, "createEffectTooltip");
	addTooltipTemplate("tooltipWounds", 0, "createWoundTooltip");
	addTooltipTemplate("tooltipStrength", 0, "createStrengthTooltip");
	addTooltipTemplate("tooltipAgility", 0, "createAgilityTooltip");
	addTooltipTemplate("tooltipConstitution", 0, "createConstitutionTooltip");
	addTooltipTemplate("tooltipIntelect", 0, "createIntelectTooltip");
	addTooltipTemplate("tooltipWillpower", 0, "createWillpowerTooltip");
	addTooltipTemplate("tooltipRecipe", 0, "createRecipeTooltip");
	addTooltipTemplate("tooltipBuildItem", 0, "createBuildItemTooltip");
}

function createAlchemyTooltip(%informer)
{
	echo("createAlchemyTooltip");
	createBaseTooltip();
	createObjectsTypesName(%informer);
	createItemsQQW(%informer);

	%pnl = createVerticalStackControl();
	%pnl.padding = 0;
	TooltipMainControl.add(%pnl);

	%textCtrl = createTextCtrl("GuiGrayTextTooltipProfile");
	%textCtrl.setText(GetMessageIDText(2561) @ ":");
	%pnl.add(%textCtrl);

	%index = 0;
	for(%index = 0; %index < 3; %index++)
	{
		%effect = %informer.getDataByIndex("objects_types", "Effect", %index);
		if (%effect !$= "")
		{
			%positive = %informer.getDataByIndex("objects_types", "EffectPositive", %index);
			createEffectCtrl(%pnl, %effect, 0, %positive);
		}
	}

	addItemDescription(%informer);
}

function createBagTooltip(%informer)
{
	echo("createBagTooltip");
	createBaseTooltip();
	createContainersName(%informer);
	createContainersWeight(%informer);

	addItemDescription(%informer);
}

function createCraftToolsEquipmentTooltip(%informer)
{
	echo("createCraftToolsEquipmentTooltip");
	createBaseTooltip();
	createObjectsTypesName(%informer);
	createItemsQWD(%informer, false);
	createItemsFeatureEffect(%informer);
	createItemRegion(%informer);

	addItemDescription(%informer);
}

function createGameplayObjectsQQWTooltip(%informer)
{
	echo("createGameplayObjectsQQWTooltip");
	createBaseTooltip();
	createObjectsTypesName(%informer);
	createItemsQQW(%informer);
	createItemsFeatureEffect(%informer);
	createItemRegion(%informer);

	addItemDescription(%informer);
}

function createGameplayObjectsQuantityWeightTooltip(%informer)
{
	echo("createGameplayObjectsQuantityWeightTooltip");
	createBaseTooltip();
	createObjectsTypesName(%informer);
	createItemsQuantityWeight(%informer);
	createItemsFeatureEffect(%informer);
	createItemRegion(%informer);

	addItemDescription(%informer);
}

function createMeleeWeaponTooltip(%informer)
{
	echo("createMeleeWeaponTooltip");
	createBaseTooltip();
	createObjectsTypesName(%informer);

	createItemsQWD(%informer, false);

	//skill strength
	%line = createHorizontalStackControl();
	TooltipMainControl.add(%line);
	%colName = createVerticalStackControl();
	%line.add(%colName);
	%colVal = createVerticalStackControl();
	%line.add(%colVal);
	addSkillInfo(%colName, %colVal, %informer);
	%strengthNeed = %informer.getData("objects_types", "UnitWeight") * 10;
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(2566), %strengthNeed);

	%typeid = %informer.getData("objects_types", "ID");
	%data_block = getShapeBaseImageData(%typeid);
	echo("%data_block.id = "  @ %data_block.Object_typeID);

	// Thrust Swing OH
	%line = createHorizontalStackControl();
	TooltipMainControl.add(%line);
	%colName = createVerticalStackControl();
	%line.add(%colName);
	%colVal = createVerticalStackControl();
	%line.add(%colVal);
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(2578), calcMaxMinThrustDmg(%informer, %data_block));
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(2579), calcMaxMinSwingDmg(%informer, %data_block));
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(2580), calcMaxMinOHDmg(%informer, %data_block));

	// Wound Fracture Stun
	%line = createHorizontalStackControl();
	TooltipMainControl.add(%line);
	%colName = createVerticalStackControl();
	%line.add(%colName);
	%colVal = createVerticalStackControl();
	%line.add(%colVal);
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(2581), %data_block.WoundMultiplier);
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(2582), %data_block.FractureMultiplier);
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(2583), %data_block.StunMultiplier);

	createItemsFeatureEffect(%informer);

	addItemDescription(%informer);
}

function createRangedWeaponTooltip(%informer)
{
	echo("createRangedWeaponTooltip");
	createBaseTooltip();
	createObjectsTypesName(%informer);

	createItemsQWD(%informer, false);

	%line = createHorizontalStackControl();
	TooltipMainControl.add(%line);
	%colName = createVerticalStackControl();
	%line.add(%colName);
	%colVal = createVerticalStackControl();
	%line.add(%colVal);

	addSkillInfo(%colName, %colVal, %informer);

	%typeid = %informer.getData("objects_types", "ID");
	%data_block = getShapeBaseImageData(%typeid);
	echo("%data_block.id = "  @ %data_block.Object_typeID);
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(2566), %data_block.StrengthNeed);
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(2567), %data_block.AgilityNeed);

	createItemsFeatureEffect(%informer);

	addItemDescription(%informer);
}

function createAmmoWeaponTooltip(%informer)
{
	createBaseTooltip();
	createObjectsTypesName(%informer);
	createItemsQQW(%informer);

	%line = createHorizontalStackControl();
	TooltipMainControl.add(%line);
	%colName = createVerticalStackControl();
	%line.add(%colName);
	%colVal = createVerticalStackControl();
	%line.add(%colVal);

	addSkillInfo(%colName, %colVal, %informer);

	%typeid = %informer.getData("objects_types", "ID");
	%data_block = getShapeBaseImageData(%typeid);
	%loadedAmmo = %data_block.loadedAmmo;

	switch$ (%loadedAmmo.hitGroupType[0])
	{
	case "Slashing": %hitGroupTypeName = GetMessageIDText(784);
	case "Piercing": %hitGroupTypeName = GetMessageIDText(786);
	case "Chopping": %hitGroupTypeName = GetMessageIDText(2496);
	case "Siege": %hitGroupTypeName = GetMessageIDText(931);
	case "Blunt": %hitGroupTypeName = GetMessageIDText(785);
	default: %hitGroupTypeName = "Unknown";
	}
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(4892), %hitGroupTypeName);

	addItemDescription(%informer);
}

function createThrowingWeaponTooltip(%informer)
{
	createBaseTooltip();
	createObjectsTypesName(%informer);
	createItemsQQW(%informer);

	%line = createHorizontalStackControl();
	TooltipMainControl.add(%line);
	%colName = createVerticalStackControl();
	%line.add(%colName);
	%colVal = createVerticalStackControl();
	%line.add(%colVal);

	addSkillInfo(%colName, %colVal, %informer);

	%typeid = %informer.getData("objects_types", "ID");
	%data_block = getShapeBaseImageData(%typeid);
	%hitGroupTypeName = %data_block.hitGroupType[0];

	switch$ (%hitGroupTypeName)
	{
	case "Slashing": %hitGroupTypeName = GetMessageIDText(784);
	case "Piercing": %hitGroupTypeName = GetMessageIDText(786);
	case "Chopping": %hitGroupTypeName = GetMessageIDText(2496);
	case "Siege": %hitGroupTypeName = GetMessageIDText(931);
	case "Blunt": %hitGroupTypeName = GetMessageIDText(785);
	default: %hitGroupTypeName = "Unknown";
	}
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(4892), %hitGroupTypeName);

	addItemDescription(%informer);
}

function createShieldsTooltip(%informer)
{
	echo("createShieldsTooltip");
	createBaseTooltip();
	createObjectsTypesName(%informer);

	createItemsQWD(%informer, false);

	%line = createHorizontalStackControl();
	TooltipMainControl.add(%line);
	%colName = createVerticalStackControl();
	%line.add(%colName);
	%colVal = createVerticalStackControl();
	%line.add(%colVal);

	addSkillInfo(%colName, %colVal, %informer);

	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(2565), calcDefence(%informer));
	createItemsFeatureEffect(%informer);

	addItemDescription(%informer);
}

function createArmorTooltip(%informer)
{
	echo("createArmorTooltip");
	createBaseTooltip();
	createObjectsTypesName(%informer);
	createItemsQWD(%informer, false);

	%line = createHorizontalStackControl();
	TooltipMainControl.add(%line);
	%colName = createVerticalStackControl();
	%line.add(%colName);
	%colVal = createVerticalStackControl();
	%line.add(%colVal);

	addSkillInfo(%colName, %colVal, %informer);

	//Damage reduction
	%pnl = createVerticalStackControl();
	%pnl.padding = 0;
	TooltipMainControl.add(%pnl);

	%textCtrl = createTextCtrl("GuiGrayTextTooltipProfile");
	%textCtrl.setText(GetMessageIDText(2584));
	%pnl.add(%textCtrl);

	%line = createHorizontalStackControl();
	%pnl.add(%line);
	%colName = createVerticalStackControl();
	%line.add(%colName);
	%colVal = createVerticalStackControl();
	%line.add(%colVal);

	%quality = %informer.getData("items", "Quality");
	%quality = %quality * 0.03;
	%quality = %quality + 7;

	%slash = %informer.getData("equipmentType", "Slash");
	%slash = %slash * 10 * %quality;
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(784), strformat("%.2f", %slash) @ " %");

	%pierce = %informer.getData("equipmentType", "Pierce");
	%pierce = %pierce * 10 * %quality;
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(786), strformat("%.2f", %pierce) @ " %");

	%chopping = %informer.getData("equipmentType", "Chopping");
	%chopping = %chopping * 10 * %quality;
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(2496), strformat("%.2f", %chopping) @ " %");

	%blunt = %informer.getData("equipmentType", "Blunt");
	%blunt = %blunt * 10 * %quality;
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(785), strformat("%.2f", %blunt) @ " %");

	%siege = %informer.getData("equipmentType", "Siege");
	%siege = %siege * 10 * %quality;
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(931), strformat("%.2f", %siege) @ " %");

	createItemsFeatureEffect(%informer);

	addItemDescription(%informer);
}

function createOutfitsTooltip(%informer)
{
	echo("createOutfitsTooltip");
	createBaseTooltip();
	createObjectsTypesName(%informer);
	createItemsQWD(%informer, false);

	%line = createHorizontalStackControl();
	TooltipMainControl.add(%line);
	%colName = createVerticalStackControl();
	%line.add(%colName);
	%colVal = createVerticalStackControl();
	%line.add(%colVal);

	addSkillInfo(%colName, %colVal, %informer);
	createItemsFeatureEffect(%informer);

	addItemDescription(%informer);
}

function createDomesticAnimalsTooltip(%informer)
{
	echo("createDomesticAnimalsTooltip");
	createBaseTooltip();
	createObjectsTypesName(%informer);
	createItemsQualityWeight(%informer);

	%line = createHorizontalStackControl();
	TooltipMainControl.add(%line);
	%colName = createVerticalStackControl();
	%line.add(%colName);
	%colVal = createVerticalStackControl();
	%line.add(%colVal);

	%createdDurability = %informer.getData("items", "CreatedDurability");
	%durability = %informer.getData("items", "Durability");
	%age = mFloor(100 * (%createdDurability - %durability) / 4);
	
	%maturity = GetMessageIDText(4532);

	if(%durability < %createdDurability * 0.3)
	{
		%maturity = GetMessageIDText(4533);
	}

	if(%durability > %createdDurability * 0.8)
	{
		%maturity = GetMessageIDText(4531);
	}

	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(1445), %age);
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(4530), %maturity);

	addItemDescription(%informer);
}

function createSimpleHorsesTooltip(%informer)
{
	echo("createSimpleHorsesTooltip");
	createBaseTooltip();
	createObjectsTypesName(%informer);
	createItemsQualityWeight(%informer);

	%line = createHorizontalStackControl();
	TooltipMainControl.add(%line);
	%colName = createVerticalStackControl();
	%line.add(%colName);
	%colVal = createVerticalStackControl();
	%line.add(%colVal);

	%typeid = %informer.getData("objects_types", "ID");
	%data_block = getHorseDataByItemTypeId(%typeid);
	echo("createHorsesTooltip %data_block.id = " @ %data_block.id);
	%maxSpeed = %data_block.maxSpeed;
	echo("createHorsesTooltip %maxSpeed = " @ %maxSpeed);
	%quality = %informer.getData("items", "Quality");
	%quality = %quality * 0.002;
	%speed = %maxSpeed * (1 + %quality);

	%createdDurability = %informer.getData("items", "CreatedDurability");
	%durability = %informer.getData("items", "Durability");
	%age = mFloor(100 * (%createdDurability - %durability) / 4);

	%maturity = GetMessageIDText(4532);

	if(%durability < %createdDurability * 0.3)
	{
		%maturity = GetMessageIDText(4533);
	}

	if(%durability > %createdDurability * 0.8)
	{
		%maturity = GetMessageIDText(4531);
	}

	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(1445), %age);
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(4530), %maturity);
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(2306), %speed);

	addItemDescription(%informer);
}

function createWarhorsesTooltip(%informer)
{
	echo("createWarhorsesTooltip");
	createBaseTooltip();
	createObjectsTypesName(%informer);
	createItemsQualityWeight(%informer);

	%line = createHorizontalStackControl();
	TooltipMainControl.add(%line);
	%colName = createVerticalStackControl();
	%line.add(%colName);
	%colVal = createVerticalStackControl();
	%line.add(%colVal);

	%typeid = %informer.getData("objects_types", "ID");
	%data_block = getHorseDataByItemTypeId(%typeid);
	echo("createHorsesTooltip %data_block.id = " @ %data_block.id);
	%maxSpeed = %data_block.maxSpeed;
	echo("createHorsesTooltip %maxSpeed = " @ %maxSpeed);
	%quality = %informer.getData("items", "Quality");
	%quality = %quality * 0.002;
	%speed = %maxSpeed * (1 + %quality);
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(2306), %speed);

	addItemDescription(%informer);
}

function createBlueprintTooltip(%informer)
{
	echo("createBlueprintTooltip");
	createBaseTooltip();

	%captionPnl = createVerticalStackControl();
	TooltipMainControl.add(%captionPnl);

	%txt = createCaptionTextCtrl();
	%captionPnl.add(%txt);
	%txt.setText(%informer.getData("objects_types", "Name"));

	%txt = createTextCtrl("GuiBoldTextTooltipProfile");
	%txt.setText(%informer.getData("items", "BlueprintRecipe"));
	%captionPnl.add(%txt);

	createItemsWeight(%informer);

	%index = 0;
	%blueprintRecipeReqName = %informer.getDataByIndex("items", "RecipeReqName", %index);
	if (%blueprintRecipeReqName !$= "")
	{
		%pnl = createVerticalStackControl();
		%pnl.padding = 0;
		TooltipMainControl.add(%pnl);

		%textCtrl = createTextCtrl("GuiGrayTextTooltipProfile");
		%textCtrl.setText("Required:");
		%pnl.add(%textCtrl);

		%line = createHorizontalStackControl();
		%pnl.add(%line);
		%colName = createVerticalStackControl();
		%line.add(%colName);
		%colVal = createVerticalStackControl();
		%line.add(%colVal);
	

		while (%blueprintRecipeReqName !$= "")
		{
			%blueprintRecipeReqFrom = %informer.getDataByIndex("items", "RecipeReqFrom", %index);
			addRowTooltipinfo(%colName, %colVal, "- " @ %blueprintRecipeReqName, "(" @ %blueprintRecipeReqFrom @ ")");

			%index++;
			%blueprintRecipeReqName = %informer.getDataByIndex("items", "RecipeReqName", %index);
		}
	}
}

function createFoodItemTooltip(%informer)
{
	createBaseTooltip();
	createObjectsTypesName(%informer);
	createItemsQQW(%informer);

	%line = createHorizontalStackControl();
	TooltipMainControl.add(%line);
	%colName = createVerticalStackControl();
	%line.add(%colName);
	%colVal = createVerticalStackControl();
	%line.add(%colVal);
	
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(4915), calcFoodValue(%informer));
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(4916), getFoodComplexity(%informer));
	createItemsFeatureEffect(%informer);
	createItemRegion(%informer);

	addItemDescription(%informer);
}

function createNameTooltip(%informer)
{
	echo("createNameTooltip");
	createBaseTooltip();
	createObjectsTypesName(%informer);
}

function createTextTooltip(%informer)
{
	echo("createTextTooltip");
	createBaseTooltip();
	TooltipMainControl.clear();

	%txt = createMLTextCtrl();
	%txt.setProfile("GuiMLCaptionTextTooltipProfile");
	%txt.setText(%informer.getData("def", ""));
	%txt.setClassNamespace("tooltipAutosizeMLText");
	TooltipMainControl.add(%txt);
}

function createEffectTooltip(%informer)
{
	echo("createEffectTooltip");
	createBaseTooltip();

	%txt = createCaptionTextCtrl();
	TooltipMainControl.add(%txt);
	%txt.setText(%informer.getData("def", ""));


	%line = createHorizontalStackControl();
	%line.padding = 16;
	TooltipMainControl.add(%line);

	%duration = %informer.getData("effect", "Duration");
	echo("%duration = " @ %duration);
	if (%duration !$= "" && %duration !$= "-")
	{
		%pnl = createIconPnl(%line, TimeIcon);
		%textCtrl = createTextCtrl("GuiLightTextTooltipProfile");
		%textCtrl.setClassNamespace("TooltipEffectDuration");
		%textCtrl.setText(%informer.getData("effect", "Duration"));
		%textCtrl.informer = %informer;
		%informer.setAutoUpdate();
		%pnl.add(%textCtrl);
	}
	else
	{
		%pnl = createHorizontalStackControl();
		%pnl.changeChildPosition = "0";
		%pnl.padding = 2;
		%line.add(%pnl);
	
		%txt = createTextCtrl("GuiLightTextTooltipProfile");
		%txt.setText(GetMessageIDText(2589) @ " ");
		%pnl.add(%txt);
	}

	%pnl = createHorizontalStackControl();
	%pnl.changeChildPosition = "0";
	%pnl.padding = 2;
	%line.add(%pnl);

	%txt = createTextCtrl("GuiGrayTextTooltipProfile");
	%txt.setText(GetMessageIDText(1392) @ " ");
	%pnl.add(%txt);
	%txt = createTextCtrl("GuiLightTextTooltipProfile");
	%txt.setText(%informer.getData("effect", "Magnitude"));
	%pnl.add(%txt);

	%des = %informer.getData("effect", "Description");
	%mltxt = createMLTextCtrl();
	%mltxt.profile = "GuiGrayMLTextTooltipProfile";
	%mltxt.setText(%des);
	%mltxt.setClassNamespace("tooltipAutosizeMLText");
	TooltipMainControl.add(%mltxt);
}

function TooltipEffectDuration::onTimer(%this)
{
	%durationVal = %this.informer.getData("effect", "Duration");
	%this.setText(%durationVal);
}

function createWoundTooltip(%informer)
{
	echo("createWoundTooltip");
	createBaseTooltip();

	%txt = createCaptionTextCtrl();
	TooltipMainControl.add(%txt);
	%txt.setText(%informer.getData("wounds", "EffectName"));


	%line = createHorizontalStackControl();
	%line.padding = 16;
	TooltipMainControl.add(%line);

	%duration = %informer.getData("wounds", "Duration");
	%pnl = createIconPnl(%line, TimeIcon);
	%textCtrl = createTextCtrl("GuiLightTextTooltipProfile");
	%textCtrl.setClassNamespace("TooltipWoundsDuration");
	%textCtrl.setText(%duration);
	%textCtrl.informer = %informer;
	%informer.setAutoUpdate();
	%pnl.add(%textCtrl);

	%pnl = createHorizontalStackControl();
	%pnl.changeChildPosition = "0";
	%pnl.padding = 2;
	%line.add(%pnl);

	%txt = createTextCtrl("GuiGrayTextTooltipProfile");
	%txt.setText(GetMessageIDText(1392) @ " ");
	%pnl.add(%txt);
	%txt = createTextCtrl("GuiLightTextTooltipProfile");
	%txt.setText(strformat("%.2f", %informer.getData("wounds", "Magnitude")));
	%pnl.add(%txt);
}

function TooltipWoundsDuration::onTimer(%this)
{
	%durationVal = %this.informer.getData("wounds", "Duration");
	%this.setText(%durationVal);
}

function createStrengthTooltip(%informer)
{
	echo("createStrengthTooltip");
	createBaseTooltip();

	%str = %informer.getData("def", "");
	%handWeight = %str * 0.1;
	addStatInfo(strformat("%.2f" ,%handWeight) @ " ", GetMessageIDText(2666));

	%equipWeight = %str / 2;
	addStatInfo(strformat("%.2f" ,%equipWeight) @ " ", GetMessageIDText(2667));
}

function createAgilityTooltip(%informer)
{
	echo("createAgilityTooltip");
	createBaseTooltip();

	%agi = %informer.getData("def", "");
	%speed = (%agi - 10) / 10;
	addStatInfo(strformat("%.2f" , %speed) @ "% ", GetMessageIDText(2668));

	%warTime = 2 * getMax(0, 1 - %agi / 80);
	addStatInfo(strformat("%.2f" , %warTime) @ " ", GetMessageIDText(2669));
}

function createConstitutionTooltip(%informer)
{
	echo("createConstitutionTooltip");
	createBaseTooltip();

	%con = %informer.getData("def", "");
	%bonus = %con - 10;
	addStatInfo("+ " @ strformat("%.2f" , %bonus) @ " ", GetMessageIDText(1101));
	addStatInfo("+ " @ strformat("%.2f" , %bonus) @ " ", GetMessageIDText(1102));
	
	%equipWeight = %con / 2;
	addStatInfo(strformat("%.2f" , %equipWeight) @ " ", GetMessageIDText(2667));
}

function createIntelectTooltip(%informer)
{
	echo("createIntelectTooltip");
	createBaseTooltip();

	%int = %informer.getData("def", "");
	%bonus = (%int - 10) * 2;

	addStatInfo(strformat("%.2f" , %bonus) @ " ", GetMessageIDText(2671));
}

function createWillpowerTooltip(%informer)
{
	echo("createWillpowerTooltip");
	createBaseTooltip();

	%wil = %informer.getData("def", "");
	%bonus = (%wil - 10);

	addStatInfo("+ " @ strformat("%.2f" , %bonus) @ " ", GetMessageIDText(1103));
	addStatInfo("+ " @ strformat("%.2f" , %bonus) @ " ", GetMessageIDText(1104));
	addStatInfo(strformat("%.2f" , %bonus * 2) @ " ", GetMessageIDText(2670));
}

function createRecipeTooltip(%informer)
{
	createBaseTooltip();

	%captionPnl = createVerticalStackControl();
	TooltipMainControl.add(%captionPnl);

	%txt = createCaptionTextCtrl();
	%txt.profile = "GuiYellowBoldTextTooltipProfile";
	%captionPnl.add(%txt);
	%txt.setText(%informer.getData("recipe", "Name"));

	%isBlueprint = %informer.getData("recipe", "IsBlueprint");
	%isDevice = %informer.getData("recipe", "IsDevice");
	%isTool = %informer.getData("recipe", "IsTool");
	if (%isBlueprint || %isDevice || %isTool)
	{
		%txt = createTextCtrl("GuiYellowTextTooltipProfile");
		if (%isBlueprint)
		{
			%txt.setText(GetMessageIDText(4920));   // Blueprint
		}
		else if (%isDevice)
		{
			%txt.setText(GetMessageIDText(4921));   // Device
		}
		else if (%isTool)
		{
			%txt.setText(GetMessageIDText(4922));   // Tool
		}
		%captionPnl.add(%txt);
	}

	%isUnmovable = %informer.getData("recipe", "IsUnmovableObject");
	%isMovable = %informer.getData("recipe", "IsMovableObject");
	%unitWeight = %informer.getData("recipe", "UnitWeight");
	if (!%isUnmovable && !%isMovable && %unitWeight > 0)
	{
		%weightPanel = createHorizontalStackControl();
		%weightIcon = new GuiBitmapCtrl()
		{
			position = "0 0";
			Extent = "19 19";
			Visible = "1";
			profile = "GuiAtlas3ImageProfile";
			imageIndex = "WeightIcon";
			centered = "1";
		};

		%weightPanel.add(%weightIcon);
		%txt = createTextCtrl("GuiYellowTextTooltipProfile", %unitWeight / 1000);
		%weightPanel.add(%txt);
		%captionPnl.add(%weightPanel);
	}

	%index = 0;
	%recipeReqName = %informer.getDataByIndex("recipe", "RecipeReqName", %index);
	if (%recipeReqName !$= "")
	{
		%pnl = createVerticalStackControl();
		%pnl.padding = 0;
		TooltipMainControl.add(%pnl);

		%line = createHorizontalStackControl();
		%pnl.add(%line);

		%colRecipePlaceholder = createVerticalStackControl();
		%line.add(%colRecipePlaceholder);

		%textCtrl = createTextCtrl("GuiGrayTextTooltipProfile", GetMessageIDText(4923));   // Required:
		%colRecipePlaceholder.add(%textCtrl);

		%colRecipeReqPlaceholder = createHorizontalStackControl();
		%colRecipePlaceholder.add(%colRecipeReqPlaceholder);

		%colRequired = createVerticalStackControl();
		%colRecipeReqPlaceholder.add(%colRequired);

		%colName = createVerticalStackControl();
		%colRecipeReqPlaceholder.add(%colName);

		%colInfluence = createVerticalStackControl();
		%line.add(%colInfluence);

		%textCtrl = createTextCtrl("GuiGrayTextTooltipProfile", GetMessageIDText(4924));   // Influence:
		%colInfluence.add(%textCtrl);

		%requiredTools = new ArrayObject();

		while (%recipeReqName !$= "")
		{
			if (%informer.getDataByIndex("recipe", "RecipeReqIsTool", %index) || %informer.getDataByIndex("recipe", "RecipeReqIsDevice", %index))
			{
				%requiredTools.push_back(%recipeReqName, %index);
			}
			else
			{
				%recipeReqQuantity = %informer.getDataByIndex("recipe", "RecipeReqQuantity", %index);
				%recipeReqInfluence = %informer.getDataByIndex("recipe", "RecipeReqInfluence", %index);

				%colRequired.add(createTextCtrl("GuiWhiteTextTooltipProfile", "- " @ %recipeReqQuantity @ "x"));
				%colName.add(createTextCtrl("GuiLightGrayTextTooltipProfile", %recipeReqName));
				%colInfluence.add(createTextCtrl("GuiLightGrayTextTooltipProfile", %recipeReqInfluence @ "%"));

				if (%informer.getDataByIndex("recipe", "RecipeReqIsRegional", %index))
				{
					%colRequired.add(createTextCtrl("GuiSmallBlueTextTooltipProfile", " "));
					if (%isBlueprint)
					{
						%colName.add(createTextCtrl("GuiSmallBlueTextTooltipProfile", GetMessageIDText(4925)));   // Resource must be regional
					}
					else
					{
						%colName.add(createTextCtrl("GuiSmallBlueTextTooltipProfile", GetMessageIDText(4956)));   // Result may inherit ingredient's region
					}
					%colInfluence.add(createTextCtrl("GuiSmallBlueTextTooltipProfile", " "));
				}
			}

			%index++;
			%recipeReqName = %informer.getDataByIndex("recipe", "RecipeReqName", %index);
		}

		// fake controls for space
		%colRequired.add(createTextCtrl("GuiSmallBlueTextTooltipProfile", " "));
		%colName.add(createTextCtrl("GuiSmallBlueTextTooltipProfile", " "));
		%colInfluence.add(createTextCtrl("GuiSmallBlueTextTooltipProfile", " "));

		// devices or tools from recipe reqs
		if (%requiredTools.count() > 0)
		{
			for(%i = 0; %i < %requiredTools.count(); %i++)
			{
				%reqToolName = %requiredTools.getKey(%i);
				%index = %requiredTools.getValue(%i);

				%isReqToolIsTool = %informer.getDataByIndex("recipe", "RecipeReqIsTool", %index);
				%isReqToolIsDevice = %informer.getDataByIndex("recipe", "RecipeReqIsDevice", %index);

				%requiredToolInfoPanel = createHorizontalStackControl();
				%txtCaption = createTextCtrl("GuiGrayTextTooltipProfile");
				if (%isReqToolIsTool)
				{
					%txtCaption.setText(GetMessageIDText(4922) @ ":");   // Tool
				}
				else if (%isReqToolIsDevice)
				{
					%txtCaption.setText(GetMessageIDText(4921) @ ":");   // Device
				}
				%requiredToolInfoPanel.add(%txtCaption);

				%textName = createTextCtrl("GuiLightGrayTextTooltipProfile", %reqToolName);
				%requiredToolInfoPanel.add(%textName);

				%colRecipePlaceholder.add(%requiredToolInfoPanel);

				%reqToolInfluence = %informer.getDataByIndex("recipe", "RecipeReqInfluence", %index);
				if (%reqToolInfluence > 0)
				{
					%colInfluence.add(createTextCtrl("GuiLightGrayTextTooltipProfile", %reqToolInfluence @ "%"));
				}
				else
				{
					%colInfluence.add(createTextCtrl("GuiLightGrayTextTooltipProfile", " "));
				}
			}
		}


		// description
		%recipeDescription = %informer.getData("recipe", "Description");
		if (%recipeDescription !$= "")
		{
			%descriptionPanel = createVerticalStackControl();
			%descriptionPanel.padding = 15;
			%descriptionPanel.TopIndent = 5;
			%descriptionPanel.setClassNamespace("ChildSizeToFitStackControl");
			TooltipMainControl.add(%descriptionPanel);

			%lineImage = new GuiControl() 
			{
				position = "0 0";
				extent = "10 1";
				minExtent = "1 1";
				profile = "GuiAtlas3TiledImageProfile";
				TopIndex0 = HLine1;
				TopIndexN = HLine4;
				canHit = "false";
			};

			%descriptionPanel.add(%lineImage);

			%descriptionText = createMLTextCtrl();
			%descriptionText.profile = "GuiMLDescrTextTooltipProfile";
			%descriptionText.setText(%recipeDescription);
			%descriptionText.setClassNamespace("tooltipAutosizeMLText");
			%descriptionPanel.add(%descriptionText);
		}
	}
}

function createBuildItemTooltip(%informer)
{
	createBaseTooltip();
	createObjectsTypesName(%informer);

	//skill
	%skillName = %informer.getData("recipe", "Skill");
	if (%skillName !$= "")
	{
		%line = createHorizontalStackControl();
		TooltipMainControl.add(%line);
		%colName = createVerticalStackControl();
		%line.add(%colName);
		%colVal = createVerticalStackControl();
		%line.add(%colVal);

		%skillLvl = %informer.getData("recipe", "SkillLvl");

		addRowTooltipinfo(%colName, %colVal, GetMessageIDText(2413), %skillName @ " (" @ %skillLvl @ ")");

		// devices and tools for create this item
		%recipeReqName = %informer.getDataByIndex("recipe", "RecipeReqName", %index);
		%index = 0;
		while (%recipeReqName !$= "")
		{
			%isReqToolIsTool = %informer.getDataByIndex("recipe", "RecipeReqIsTool", %index);
			%isReqToolIsDevice = %informer.getDataByIndex("recipe", "RecipeReqIsDevice", %index);

			if (%isReqToolIsDevice)
			{
				addRowTooltipinfo(%colName, %colVal, GetMessageIDText(4921), %recipeReqName);
			}
			else if (%isReqToolIsTool)
			{
				addRowTooltipinfo(%colName, %colVal, GetMessageIDText(4922), %recipeReqName);
			}

			%index++;
			%recipeReqName = %informer.getDataByIndex("recipe", "RecipeReqName", %index);
		}

		// starting tool
		// %isDevice = %informer.getData("recipe", "IsStartingToolIsDevice");
		// %isTool = %informer.getData("recipe", "IsStartingToolIsTool");
		// if (%isDevice)
		// {
		// 	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(4921), %informer.getData("recipe", "StartingToolName"));
		// }
		// else if (%isTool)
		// {
		// 	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(4922), %informer.getData("recipe", "StartingToolName"));
		// }
	}

	addItemDescription(%informer);
}

function createUsableItemsMeleeWeaponTooltip(%informer)
{
	echo("createUsableItemsMeleeWeaponTooltip");
	createBaseTooltip();
	createObjectsTypesName(%informer);

	%line = createHorizontalStackControl();
	%line.padding = 16;
	TooltipMainControl.add(%line);

	//Weight
	createIconText(%line, WeightIcon, %informer.getData("objects_types", "UnitWeight"));
	//Default quality
	createIconText(%line, QualityIcon, 50);

	//skill strength
	%line = createHorizontalStackControl();
	TooltipMainControl.add(%line);
	%colName = createVerticalStackControl();
	%line.add(%colName);
	%colVal = createVerticalStackControl();
	%line.add(%colVal);
	addSkillInfo(%colName, %colVal, %informer);
	%strengthNeed = %informer.getData("objects_types", "UnitWeight") * 10;
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(2566), %strengthNeed);

	%typeid = %informer.getData("objects_types", "ID");
	%data_block = getShapeBaseImageData(%typeid);
	echo("%data_block.id = "  @ %data_block.Object_typeID);

	// Thrust Swing OH
	%line = createHorizontalStackControl();
	TooltipMainControl.add(%line);
	%colName = createVerticalStackControl();
	%line.add(%colName);
	%colVal = createVerticalStackControl();
	%line.add(%colVal);
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(2578), calcMaxMinThrustDmg(%informer, %data_block, true));
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(2579), calcMaxMinSwingDmg(%informer, %data_block, true));
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(2580), calcMaxMinOHDmg(%informer, %data_block, true));

	// Wound Fracture Stun
	%line = createHorizontalStackControl();
	TooltipMainControl.add(%line);
	%colName = createVerticalStackControl();
	%line.add(%colName);
	%colVal = createVerticalStackControl();
	%line.add(%colVal);
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(2581), %data_block.WoundMultiplier);
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(2582), %data_block.FractureMultiplier);
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(2583), %data_block.StunMultiplier);

	createItemsFeatureEffect(%informer);

	addItemDescription(%informer);
}

function createUsableItemsRangedWeaponTooltip(%informer)
{
	createBaseTooltip();
	createObjectsTypesName(%informer);

	%line = createHorizontalStackControl();
	%line.padding = 16;
	TooltipMainControl.add(%line);

	//Weight
	createIconText(%line, WeightIcon, %informer.getData("objects_types", "UnitWeight"));

	%line = createHorizontalStackControl();
	TooltipMainControl.add(%line);
	%colName = createVerticalStackControl();
	%line.add(%colName);
	%colVal = createVerticalStackControl();
	%line.add(%colVal);

	addSkillInfo(%colName, %colVal, %informer);

	%typeid = %informer.getData("objects_types", "ID");
	%data_block = getShapeBaseImageData(%typeid);
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(2566), %data_block.StrengthNeed);
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(2567), %data_block.AgilityNeed);

	addItemDescription(%informer);
}

function createUsableItemsArmorTooltip(%informer)
{
	echo("createArmorTooltip");
	createBaseTooltip();
	createObjectsTypesName(%informer);

	%line = createHorizontalStackControl();
	%line.padding = 16;
	TooltipMainControl.add(%line);

	//Weight
	createIconText(%line, WeightIcon, %informer.getData("objects_types", "UnitWeight"));
	//Default quality
	createIconText(%line, QualityIcon, 50);

	%line = createHorizontalStackControl();
	TooltipMainControl.add(%line);
	%colName = createVerticalStackControl();
	%line.add(%colName);
	%colVal = createVerticalStackControl();
	%line.add(%colVal);

	addSkillInfo(%colName, %colVal, %informer);

	//Damage reduction
	%pnl = createVerticalStackControl();
	%pnl.padding = 0;
	TooltipMainControl.add(%pnl);

	%textCtrl = createTextCtrl("GuiGrayTextTooltipProfile");
	%textCtrl.setText(GetMessageIDText(2584));
	%pnl.add(%textCtrl);

	%line = createHorizontalStackControl();
	%pnl.add(%line);
	%colName = createVerticalStackControl();
	%line.add(%colName);
	%colVal = createVerticalStackControl();
	%line.add(%colVal);

	%quality = 8.5;

	%slash = %informer.getData("equipmentType", "Slash");
	%slash = %slash * 10 * %quality;
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(784), strformat("%.2f", %slash) @ " %");

	%pierce = %informer.getData("equipmentType", "Pierce");
	%pierce = %pierce * 10 * %quality;
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(786), strformat("%.2f", %pierce) @ " %");

	%chopping = %informer.getData("equipmentType", "Chopping");
	%chopping = %chopping * 10 * %quality;
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(2496), strformat("%.2f", %chopping) @ " %");

	%blunt = %informer.getData("equipmentType", "Blunt");
	%blunt = %blunt * 10 * %quality;
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(785), strformat("%.2f", %blunt) @ " %");

	%siege = %informer.getData("equipmentType", "Siege");
	%siege = %siege * 10 * %quality;
	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(931), strformat("%.2f", %siege) @ " %");

	createItemsFeatureEffect(%informer);

	addItemDescription(%informer);
}

function createUsableItemsShieldsTooltip(%informer)
{
	echo("createShieldsTooltip");
	createBaseTooltip();
	createObjectsTypesName(%informer);

	%line = createHorizontalStackControl();
	%line.padding = 16;
	TooltipMainControl.add(%line);

	//Weight
	createIconText(%line, WeightIcon, %informer.getData("objects_types", "UnitWeight"));
	//Default quality
	createIconText(%line, QualityIcon, 50);

	%line = createHorizontalStackControl();
	TooltipMainControl.add(%line);
	%colName = createVerticalStackControl();
	%line.add(%colName);
	%colVal = createVerticalStackControl();
	%line.add(%colVal);

	addSkillInfo(%colName, %colVal, %informer);

	addRowTooltipinfo(%colName, %colVal, GetMessageIDText(2565), calcDefence(%informer, true));
	createItemsFeatureEffect(%informer);

	addItemDescription(%informer);
}

function createUsableItemsDefaultTooltip(%informer)
{
	createBaseTooltip();
	createObjectsTypesName(%informer);

	%line = createHorizontalStackControl();
	%line.padding = 16;
	TooltipMainControl.add(%line);

	//Weight
	createIconText(%line, WeightIcon, %informer.getData("objects_types", "UnitWeight"));
	addItemDescription(%informer);
}

function calcMaxMinDmg(%informer, %data_block, %index, %factor, %isDefault)
{
	echo("calcMaxMinDmg %index = " @ %index @ " %factor = " @ %factor);
	
	echo("calcMaxMinDmg %Weight = " @ %weight);

	echo("%isDefault = " @ %isDefault);
	if (%isDefault)
	{
		%weight = %informer.getData("objects_types", "UnitWeight");
		%quality = 0.5;
	}
	else
	{
	%weight = %informer.getData("items", "Weight");
	%quality = %informer.getData("items", "Quality");
	%quality = %quality * 0.01;
	}
	echo("calcMaxMinDmg %quality = " @ %quality);

	%hitgroupindex = %data_block.hitDirection[%index];
	echo("calcMaxMinDmg %data_block.hitDirection[%index] = " @ %data_block.hitDirection[%index]);
	echo("calcMaxMinDmg %hitgroupindex.x = " @ %hitgroupindex.x);

	%hitgroup = %data_block.hitGroupDmgLevel[%hitgroupindex.x];
	echo("calcMaxMinDmg %hitgroup = " @ %hitgroup);

	%minDmg = mSqrt(%weight) * (1 + %quality) * %hitgroup * 1.2 * 1.25;
	%maxDmg = mSqrt(%weight*%factor) * (1 + %quality) * %hitgroup * 1.2 * 1.25;

	switch$ (%data_block.hitGroupType[%hitgroupindex.x])
	{
	case "Slashing": %hitGroupTypeName = GetMessageIDText(784);
	case "Piercing": %hitGroupTypeName = GetMessageIDText(786);
	case "Chopping": %hitGroupTypeName = GetMessageIDText(2496);
	case "Siege": %hitGroupTypeName = GetMessageIDText(931);
	case "Blunt": %hitGroupTypeName = GetMessageIDText(785);
	}

	%result = strformat("%.2f", %minDmg) @ " â€” " @ strformat("%.2f", %maxDmg) @ " " @ %hitGroupTypeName;
	return %result;
}

function calcMaxMinThrustDmg(%informer, %data_block, %isDefault)
{
	echo("calcMaxMinThrustDmg");
	return calcMaxMinDmg(%informer, %data_block, 0, 25, %isDefault);
}

function calcMaxMinSwingDmg(%informer, %data_block, %isDefault)
{
	echo("calcMaxMinSwingDmg");
	return calcMaxMinDmg(%informer, %data_block, 2, 80, %isDefault);
}

function calcMaxMinOHDmg(%informer, %data_block, %isDefault)
{
	echo("calcMaxMinOHDmg");
	return calcMaxMinDmg(%informer, %data_block, 1, 80, %isDefault);
}

function calcDefence(%informer, %isDefault)
{
	echo("calcDefence");
	if (%isDefault)
	{
		%quality = 50;
		%weight = %informer.getData("objects_types", "UnitWeight");
	}
	else
	{
	%quality = %informer.getData("items", "Quality");
	echo("calcDefence %quality = " @ %quality);
	%weight = %informer.getData("items", "Weight");
	echo("calcDefence %Weight = " @ %weight);
	}
	%sqrtQuality = mSqrt(%quality)/10;
	echo("calcDefence mSqrt(%quality)/10 = " @ %sqrtQuality);
	%minQuality = getMin(0.3, %sqrtQuality);
	echo("calcDefence minQuality = " @ %minQuality);

	%sqrtWeight = mSqrt(%weight)/3;
	echo("calcDefence mSqrt(%weight)/3 = " @ %sqrtWeight);
	%minWeight = getMin(1, %sqrtWeight);
	echo("calcDefence minWeight = " @ %minWeight);

	%result = %minQuality * %minWeight * 0.5 * 100;
	echo("calcDefence result = " @ %result);

	return %result;
}

function calcCritWeaponWeight(%informer)
{
	echo("calcCritWeaponWeight");
	%weight = %informer.getData("items", "Weight");
	echo("calcCritWeaponWeight %Weight = " @ %weight);

	%result = %weight * 10;
	echo("calcCritWeaponWeight result = " @ %result);

	return %result;
}

function getFoodComplexity(%informer)
{
	%typeid = %informer.getData("objects_types", "ParentID");
	switch (%typeid)
	{
		case 227 : return 5;
		case 228 : return 4;
		case 229 : return 3;
		case 230 : return 2;
		case 231 : return 1;
		case 232 : return 1;
		default : return 1;
	}
}

function calcFoodValue(%informer)
{
	%quality = %informer.getData("items", "Quality");
	%complexity = getFoodComplexity(%informer);
	%typeid = %informer.getData("objects_types", "ParentID");
	
	// Workaround for OtherFood (ParentID=232) items, since they're have complexity=1
	// but a special HungerRate calculation:
	// `foodHungerRate = (F32)foodQuality / mSqrt((F32)10);`
	if (%typeid == 232)
	{
		%complexity = 10;
	}

	%sqrtComplexity = mSqrt(%complexity);
	%result = mFloatLength(%quality / %sqrtComplexity, 2);

	return %result;
}

function createCommonTooltipProfiles()
{
	if (!isObject(GuiYellowTextTooltipProfile))
	{
		new GuiControlProfile(GuiYellowTextTooltipProfile: GuiTextTooltipProfile)
		{
			fontColor = "195 129 0";
		};
	}

	if (!isObject(GuiYellowBoldTextTooltipProfile))
	{
		new GuiControlProfile(GuiYellowBoldTextTooltipProfile: GuiCaptionTextTooltipProfile)
		{
			fontColor = "195 129 0";
		};
	}

	if (!isObject(GuiWhiteTextTooltipProfile))
	{
		new GuiControlProfile(GuiWhiteTextTooltipProfile: GuiTextTooltipProfile)
		{
			fontType = "Open Sans Bold Italic";
			fontColor = "255 255 255";
		};
	}

	if (!isObject(GuiLightGrayTextTooltipProfile))
	{
		new GuiControlProfile(GuiLightGrayTextTooltipProfile: GuiTextTooltipProfile)
		{
			fontColor = "204 204 204";
		};
	}

	if (!isObject(GuiSmallBlueTextTooltipProfile))
	{
		new GuiControlProfile(GuiSmallBlueTextTooltipProfile: GuiTextTooltipProfile)
		{
			fontColor = "51 153 153";
			fontSize = "16";
		};
	}
}

function deleteCommonTooltipProfiles()
{
	if (isObject(GuiYellowTextTooltipProfile))
	{
		GuiYellowTextTooltipProfile.safeDeleteObject();
	}

	if (isObject(GuiWhiteTextTooltipProfile))
	{
		GuiWhiteTextTooltipProfile.safeDeleteObject();
	}

	if (isObject(GuiLightGrayTextTooltipProfile))
	{
		GuiLightGrayTextTooltipProfile.safeDeleteObject();
	}

	if (isObject(GuiYellowBoldTextTooltipProfile))
	{
		GuiYellowBoldTextTooltipProfile.safeDeleteObject();
	}

	if (isObject(GuiSmallBlueTextTooltipProfile))
	{
		GuiSmallBlueTextTooltipProfile.safeDeleteObject();
	}
}