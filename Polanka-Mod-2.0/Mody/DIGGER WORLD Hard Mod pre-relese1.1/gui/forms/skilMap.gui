//-----------------------------------------------------------------------------
// Craftsman & Marksman: Life is feudal
//-----------------------------------------------------------------------------

singleton GuiControlProfile(GuiSkilcapTextProfile: GuiBaseCaptionProfile)
{
	fontSize = 22;
	autoSizeWidth = true;
	fontColor = "206 206 206";
};

singleton GuiControlProfile(GuiSkillcapNumProfile: GuiBaseCaptionProfile)
{
	fontSize = 18;
	fontColor = "255 255 255";
};

singleton GuiControlProfile(GuiSkillcapRightNumProfile: GuiSkillcapNumProfile)
{
	justify = "right";
};

singleton GuiControlProfile(GuiSkillItemValProfile : GuiBaseTextBoldProfile)
{
	fontSize = 15;
	fontColor = "153 153 153";
	justify = "center";
};

function createSkillMapPnl(%parent, %skillGroup)
{
	%hasBooster = !$cmYO && (%skillGroup != 3);

	%map = "";
	if (%skillGroup == 1)
	{
		%map = "gui/images/skillcraftmap.png";
	}
	else if (%skillGroup == 2)
	{
		%map = "gui/images/skillcombatmap.png";
	}
	else if (%skillGroup == 3)
	{
		%map = "gui/images/skillminormap.png";
	}

	%guiSkillInfoPnl = new GuiContainer()
	{
		docking = Right;
		position = "500 0";
		extent = "431 1080";
		Profile = "GuiBaseTextProfile";

		new GuiBitmapCtrl()
		{
			position = "-1489 0";
			extent = "1920 1080";
			bitmap = "gui/images/Main_back_ornament.png";
			profile = "GuiBaseTextProfile";
			horizSizing = "left";
			vertSizing = "top";
		};

		new GuiBitmapCtrl()
		{
			position = "0 0";
			extent = "36 1080";
			profile = "GuiAtlas1ImageProfile";
			imageIndex = FringeLeftShadow;
			horizSizing = "right";
			vertSizing = "height";
			wrap = true;
		};
	};
	%parent.add(%guiSkillInfoPnl);

	%skillInfo = createSkillInfo();
	%guiSkillInfoPnl.add(%skillInfo);

	%guiSkillMapPnl = new GuiContainer()
	{
		docking = Client;
		extent = "500 500";
		Profile = "GuiBaseTextProfile";

		new GuiBitmapCtrl()
		{
			position = "0 0";
			extent = "596 607";
			bitmap = %map;
			profile = "GuiBaseTextProfile";
		};
	};

	%parent.add(%guiSkillMapPnl);

	%bar = new cmBarWidget()
	{
		position = "9 441";
		extent = %hasBooster ? "511 40" : "576 40";
		MinExtent = "20 30";
		profile = "GuiBarProfile";
		leftHiddenBar = 0;
		rightBarOffset = 7;
		LeftBarOffset = 7;
		vertSizing = "top";

		backgroundLeftIndex = SkillcapBorderLeft;
		backgroundCenterIndex = SkillcapBorderCenter;
		backgroundRightIndex = SkillcapBorderRight;
		firstBarLeftIndex = SkillcapBarLeft;
		firstBarCenterIndex = SkillcapBarCenter;
		firstBarRightIndex = SkillcapBarRight;
		internalName = skillcapBar;

		new GuiTextCtrl()
		{
			position = "20 0";
			extent = "100 40";
			Profile = "GuiSkillcapNumProfile";
			internalName = curSkillcapVal;
			text = "0";
		};

		new GuiTextCtrl()
		{
			position = %hasBooster ? "389 0" : "453 0";
			extent = "100 40";
			Profile = "GuiSkillcapRightNumProfile";
			internalName = maxSkillcapVal;
			text = "0";
		};

		new GuiTextCtrl()
		{
			position = "0 0";
			extent = "100 40";
			HorizSizing = "center";
			vertSizing = "center";
			Profile = "GuiSkilcapTextProfile";
			text = GetMessageIDText(1122); //Skillcap
		};
	};
	
	%guiSkillMapPnl.add(%bar);
	
	if (%hasBooster)
	{
		%boostBtn = new GuiControl()
		{		
			position = "521 534";
			extent = "65 66";
			class = "SkillBoosterControl";
			skillGroup = %skillGroup;
				
			new GuiButtonCtrl()
			{
				internalName = "boosterButton";
				position = "0 0";
				extent = "65 66";
				visible = "true";
				profile = "GuiAtlas2ImageProfile";
				command = "PremiumSkillcap();";
				imageIndex = BtnBooster;
			};
			
			new GuiBitmapCtrl()
			{
				internalName = "boosterTimer";
				extent = "70 70";
				position = "0 0";
				canHit = "false";
				visible = "false";
				profile = "GuiAtlas2ImageProfile";
				imageIndex = BtnBoosterTimer;
				
				new GuiMLTextCtrl() 
				{
					position = "2 26";
					extent = "70 2";
					Profile = "GuiText7pxProfile";
					text = "";
				};
			};
		};

		%guiSkillMapPnl.add(%boostBtn);
	}
	
	addSkills(%guiSkillMapPnl, %skillGroup, %skillInfo);
}

