//-----------------------------------------------------------------------------
function createQuestTasksWindow()
{
	createQuestTasksWindowProfiles();

	if (isObject(GuiQuestTasksWnd))
	{
		return GuiQuestTasksWnd;
	}

	new GuiWindowCtrl(GuiQuestTasksWnd)
	{
		text = GetMessageIDText(4413);
		forGuiEdit = "1";
		resizeWidth = "1";
		resizeHeight = "1";
		canMove = "1";
		canClose = "0";
		canMinimize = "0";
		canMaximize = "0";
		canCollapse = "0";
		canHideOnFreelook = "1";
		canLock = "0";
		canSetup = "0";
		edgeSnap = "1";
		margin = "0 0 0 0";
		padding = "0 0 0 0";
		anchorTop = "1";
		anchorBottom = "0";
		anchorLeft = "1";
		anchorRight = "0";
		extent = "418 400";
		position = PlayGui.extent.x - 430 @ " 10";
		minExtent = "200 200";
		profile = "GuiWindowProfile";
		visible = "1";
		active = "1";
		isContainer = "1";
		closeCommand ="closeQuestTasksWindow();";
		locked = "1";
		saveFileName = "QuestTasksWnd";
		horizSizing = "left";

		// client area
		new GuiContainer()
		{
			docking = Client;
			position = "0 0";
			extent = "0 0";
			canSaveDynamicFields = "0";
			enabled = "1";
			profile = "QuestTasksContentProfile";
			canSave = "1";
			visible = "1";
			canHit = "true";

			// background
			new GuiBitmapCtrl()
			{
				position = "0 0";
				extent = "100% 100%";
				profile = "QuestTasksAtlas3Profile";
				visible = "1";
				canHit = "false"; 
				imageIndex = "LearningWindowBackground";
				opacity = 0.7;
			};

			new GuiContainer()
			{
				docking = Top;
				position = "0 0";
				extent = "10 17";
				enabled = "1";
				profile = "QuestTasksContentProfile";
				visible = "1";
				canHit = "false";
			};

			new GuiContainer()
			{
				docking = Bottom;
				position = "0 0";
				extent = "10 28";
				enabled = "1";
				profile = "QuestTasksContentProfile";
				visible = "1";
				canHit = "false";
			};

			// new GuiContainer()
			// {
				// docking = Left;
				// position = "0 0";
				// extent = "10 10";
				// enabled = "1";
				// profile = "QuestTasksContentProfile";
				// visible = "1";
				// canHit = "false";
			// };

			new GuiContainer()
			{
				docking = Right;
				position = "0 0";
				extent = "10 10";
				enabled = "1";
				profile = "QuestTasksContentProfile";
				visible = "1";
				canHit = "false";
			};

			new GuiContainer()
			{
				docking = Client;
				position = "0 0";
				extent = "1 1";
				enabled = "1";
				profile = "QuestTasksContentProfile";
				visible = "1";
				canHit = "true";

				new GuiScrollCtrl()
				{
					hScrollBar = "alwaysOff";
					// position = "7 42";
					// extent = "404 318";
					position = "0 0";
					extent = "100% 100%";
					profile = "QuestTasksScrollProfile";
					constantThumbHeight = true;
					childMargin = "0 0";
					trackOffset = 8;
					addContentWidth = 30;

					new GuiStackControl()
					{
						position = "0 0";
						extent = "100% 0";
						horizSizing = "width";
						internalName = "questsListCtrl";
						Stacking = Vertical;
						profile = "QuestTasksContentProfile";
						vertStacking = "Top to Bottom";
						padding = 2;
						dynamicSize = "1";
						dynamicNonStackExtent = "0";
						changeChildPosition = "1";
						changeChildSizeToFit = "1";
					};
				};
			};
		};
	};

	PlayGui.add(GuiQuestTasksWnd);
	return GuiQuestTasksWnd;
}
//-----------------------------------------------------------------------------
function closeQuestTasksWindow()
{
	if (isObject(GuiQuestTasksWnd))
	{
		PlayGui.remove(GuiQuestTasksWnd);
		GuiQuestTasksWnd.safeDeleteObject();
	}
}
//-----------------------------------------------------------------------------
function createQuestTasksWindowProfiles()
{
	if (!isObject(QuestTasksMainWindowProfile))
	{
		new GuiControlProfile(QuestTasksMainWindowProfile: GuiWindowProfile)
		{
			globalImageIndex = "atlas3";
		};
	}

	if (!isObject(QuestTasksAtlas3Profile))
	{
		new GuiControlProfile(QuestTasksAtlas3Profile: GuiBaseTextProfile)
		{
			globalImageIndex = "atlas3";
		};
	}

	if (!isObject(QuestTasksScrollProfile))
	{
		new GuiControlProfile(QuestTasksScrollProfile: GuiScrollProfile)
		{
			globalImageIndex = "atlas1";
			opaque = false;
		};
	}

	if (!isObject(QuestTasksContentProfile))
	{
		new GuiControlProfile(QuestTasksContentProfile: GuiBaseTextProfile)
		{
		};
	}

	if (!isObject(QuestTasksQuestRootButtonProfile))
	{
		new GuiControlProfile(QuestTasksQuestRootButtonProfile: QuestTasksAtlas3Profile)
		{
		};
	}

	if (!isObject(QuestTasksQuestNameTextProfile))
	{
		new GuiControlProfile(QuestTasksQuestNameTextProfile : GuiBaseTextProfile)
		{
			textOffset = "45 0";
		};
	}

	if (!isObject(QuestTasksNameTextProfile))
	{
		new GuiControlProfile(QuestTasksNameTextProfile : GuiBaseTextProfile)
		{
		};
	}

	if (!isObject(QuestTasksRootContentProfile))
	{
		new GuiControlProfile(QuestTasksRootContentProfile : GuiBaseTextProfile)
		{
		};
	}

	if (!isObject(QuestTaskDescriptionTextProfile))
	{
		new GuiControlProfile(QuestTaskDescriptionTextProfile: GuiBaseTextProfile)
		{
			fontSize = 20;
			fontColor = "153 153 153";
		};
	}

}
//-----------------------------------------------------------------------------
function deleteQuestTasksWindowProfiles()
{
	if (isObject(QuestTasksMainWindowProfile))
	{
		QuestTasksMainWindowProfile.safeDeleteObject();
	}

	if (isObject(QuestTasksAtlas3Profile))
	{
		QuestTasksAtlas3Profile.safeDeleteObject();
	}

	if (isObject(QuestTasksScrollProfile))
	{
		QuestTasksScrollProfile.safeDeleteObject();
	}

	if (isObject(QuestTasksContentProfile))
	{
		QuestTasksContentProfile.safeDeleteObject();
	}

	if (isObject(QuestTasksQuestRootButtonProfile))
	{
		QuestTasksQuestRootButtonProfile.safeDeleteObject();
	}

	if (isObject(QuestTasksQuestNameTextProfile))
	{
		QuestTasksQuestNameTextProfile.safeDeleteObject();
	}

	if (isObject(QuestTasksRootContentProfile))
	{
		QuestTasksRootContentProfile.safeDeleteObject();
	}

	if (isObject(QuestTasksNameTextProfile))
	{
		QuestTasksNameTextProfile.safeDeleteObject();
	}

}
//-----------------------------------------------------------------------------
function questTask_addQuest(%questId, %questName, %questIcon)
{
	if (!isObject(GuiQuestTasksWnd))
	{
		echo("quest tasks window not found");
		return;
	}

	%itemsCount = GuiQuestTasksWnd-->questsListCtrl.getCount();
	for (%i = 0; %i < %itemsCount; %i++)
	{
		%questItem = GuiQuestTasksWnd-->questsListCtrl.getObject(%i);

		if (%questItem.questId == %questId)
		{
			return; // quest already exists
		}
	}

	%questRoot = new GuiStackControl()
	{
		position = "0 0";
		extent = "43 1";
		Stacking = Vertical;
		profile = "QuestTasksRootContentProfile";
		vertStacking = "Top to Bottom";
		padding = 0;
		dynamicSize = "1";
		dynamicNonStackExtent = "0";
		changeChildPosition = "1";
		changeChildSizeToFit = "1";
		internalName = "questRoot";
		questId = %questId;

		// кнопка активации квеста
		new GuiButtonCtrl()
		{
			text = "";
			groupNum = "-1";
			buttonType = "PushButton";
			position = "0 0";
			extent = "43 26";
			minExtent = "8 2";
			profile = "QuestTasksQuestRootButtonProfile";
			visible = "1";
			active = "1";
			command = "GuiQuestTasksWnd.onQuestClicked(" @  %questId @ ");";
			horizSizing = "width";
			isActiveQuest = "0";
			internalName = "questCaptionCtrl";
			class = "QuestTasksRootButtonCtrl";

			// оверлап над кнопкой
			new GuiTilingBitmapOverlap() 
			{
				position = "0 0";
				extent = "23 26";
				canHit = "false";
				profile = "QuestTasksAtlas3Profile";
				horizSizing = "width";
				visible = "0";
				TopLeftIndex = SlimMenuItem2MouseOverLeft;
				TopIndex0 = SlimMenuItem2MouseOverCenter0;
				TopIndexN = SlimMenuItem2MouseOverCenter1;
				TopRightIndex = SlimMenuItem2MouseOverRight;
				internalName = "MouseOverPnl";
				opaque = 0.7;
			};

			// иконка квеста
			new GuiBitmapCtrl()
			{
				position = "12 0";
				extent = "29 29";
				profile = "QuestTasksContentProfile";
				visible = "0";
				active = "1";
				centered = "0";
				canHit = "false";
				wrap = "0";
				resize = "1";
				bitmap = %questIcon;
				internalName = "questIcon";
			};

			// название квеста
			new GuiMLTextCtrl()
			{
				text = getInactiveQuestNameText(%questName);
				position = "0 2";
				extent = "43 1";
				profile = "QuestTasksQuestNameTextProfile";
				canHit = "false"; 
				visible = "1";
				renderDotIfTextDoesNotFit = "1";
				horizSizing = "width";
				internalName = "questName";
				bottomOffset = "0";
				class = "QuestNameMLTextCtrl";
				originalQuestName = %questName;
				isMouseOver = "0";
			};
		};

		// таски квеста
		new GuiStackControl()
		{
			position = "0 0";
			extent = "43 1";
			minExtent = "1 1";
			Stacking = Vertical;
			profile = "QuestTasksContentProfile";
			vertStacking = "Top to Bottom";
			padding = 0;
			dynamicSize = "1";
			dynamicNonStackExtent = "0";
			changeChildPosition = "1";
			changeChildSizeToFit = "1";
			visible = "0";
			internalName = "questTasks";
		};

		new GuiControl()
		{
			position = "0 0";
			extent = "43 7";
			minExtent = "1 1";
			profile = "QuestTasksContentProfile";
			visible = "0";
			horizSizing = "width";
			internalName = "questTasksStub";
		};
	};


	GuiQuestTasksWnd-->questsListCtrl.add(%questRoot);
}
//-----------------------------------------------------------------------------
function questTask_addQuestTask(%questId, %taskId, %taskName, %taskStatus, %taskDescription, %hasMapButton, %hasTutorialHint)
{
	if (!isObject(GuiQuestTasksWnd))
	{
		return;
	}

	%itemsCount = GuiQuestTasksWnd-->questsListCtrl.getCount();

	for (%i = 0; %i < %itemsCount; %i++)
	{
		%questItem = GuiQuestTasksWnd-->questsListCtrl.getObject(%i);

		if (%questItem.questId == %questId)
		{

			%tasksCount = %questItem-->questTasks.getCount();
			for (%t = 0; %t < %tasksCount; %t++)
			{
				%taskItem = %questItem-->questTasks.getObject(%t);
				if (%taskItem.taskId == %taskId)
				{
					%taskItem.safeDeleteObject();
				}
			}

			%ctrl = new GuiControl()
			{
				position = "0 0";
				extent = "126 29";
				minExtent = "1 1";
				profile = "QuestTasksContentProfile";
				visible = "1";
				horizSizing = "width";
				taskStatus = %taskStatus;
				taskName = %taskName;
				taskId = %taskId;
				taskDescription = %taskDescription;
				hasMapButton = %hasMapButton;
				hasTutorialHint = %hasTutorialHint;
				
				// bullet
				new GuiBitmapCtrl()
				{
					position = "50 6";
					extent = "16 16";
					profile = "GuiAtlas3ImageProfile";
					visible = "1";
					imageIndex = "TaskCheckBoxDisabled";
					internalName = "taskBullet";
				};

				// название таска
				new GuiMLTextCtrl()
				{
					text = "";
					position = "69 5";
					extent = "20 1";
					profile = "QuestTasksNameTextProfile";
					canHit = "false"; 
					visible = "1";
					horizSizing = "width";
					internalName = "taskName";
					bottomOffset = "0";
					class = "QuestTaskNameMLTextCtrl";
				};

				// кнопка вывода информации о таске
				new GuiButtonCtrl()
				{
					text = "";
					groupNum = "-1";
					buttonType = "PushButton";
					position = "12 0";
					extent = "29 29";
					minExtent = "8 2";
					profile = "QuestTasksAtlas3Profile";
					visible = "1";
					active = "1";
					command = "questTask_ShowQuestTaskDescription(" @ %taskDescription @ ");";
					imageIndex = "InfoBtnNormal";
					horizSizing = "left";
					internalName = "taskInfoBtn";
				};

				// show/hide on map
				new GuiButtonCtrl()
				{
					text = "";
					groupNum = "-1";
					buttonType = "ToggleButton";
					position = "43 0";
					extent = "29 29";
					minExtent = "8 2";
					profile = "QuestTasksAtlas3Profile";
					visible = "0";
					active = "0";
					command = "$thisControl.toggle(" @ %questId @ "," @ %taskId @ ");";
					imageIndex = "LocationMapBtnNormal";
					horizSizing = "left";
					class = GuiTaskOnMapButton;
					internalName = "taskOnMapBtn";
				};
				
				// tutorial hint
				new GuiButtonCtrl()
				{
					text = "";
					groupNum = "-1";
					buttonType = "PushButton";
					position = "74 0";
					extent = "29 29";
					minExtent = "8 2";
					profile = "QuestTasksAtlas3Profile";
					visible = "0";
					active = "0";
					command = "questTask_ShowQuestTaskTutorialHint(" @%questId@ "," @ %taskId @ ");";
					imageIndex = "LocationMapBtnNormal";
					horizSizing = "left";
					internalName = "taskTutorialHintBtn";
				};
			};

			if (%hasMapButton)
			{
				%ctrl-->taskOnMapBtn.setVisible(true);
				%ctrl-->taskOnMapBtn.setActive(true);
			}
			
			if (%hasTutorialHint)
			{
				%ctrl-->taskTutorialHintBtn.setVisible(true);
				%ctrl-->taskTutorialHintBtn.setActive(true);
			}

			questTasks_updateQuestTaskProps(%ctrl);
			%questItem-->questTasks.add(%ctrl);
			return;
		}
	}
}
//-----------------------------------------------------------------------------
function questTasks_setQuestActive(%questId)
{
	if (!isObject(GuiQuestTasksWnd))
	{
		echo("Quest tasks window not found");
		return;
	}

	GuiQuestTasksWnd.setActiveQuest(%questId);
}
//-----------------------------------------------------------------------------
function questTask_setQuestTaskStatus(%questId, %taskId, %taskStatus)
{
	if (!isObject(GuiQuestTasksWnd))
	{
		echo("Quest tasks window not found");
		return;
	}

	GuiQuestTasksWnd.setQuestTaskStatus(%questId, %taskId, %taskStatus);
}
//-----------------------------------------------------------------------------
function questTasks_deleteQuest(%questId)
{
	if (!isObject(GuiQuestTasksWnd))
	{
		echo("Quest tasks window not found");
		return;
	}

	%itemsCount = GuiQuestTasksWnd-->questsListCtrl.getCount();

	for (%i = 0; %i < %itemsCount; %i++)
	{
		%questItem = GuiQuestTasksWnd-->questsListCtrl.getObject(%i);

		if (%questItem.questId == %questId)
		{
			GuiQuestTasksWnd-->questsListCtrl.remove(%questItem);
			return;
		}
	}
}
//-----------------------------------------------------------------------------
function questTasks_setSingleQuestActive()
{
	if (!isObject(GuiQuestTasksWnd))
	{
		echo("Quest tasks window not found");
		return;
	}

	%itemsCount = GuiQuestTasksWnd-->questsListCtrl.getCount();
	if (%itemsCount == 1)
	{
		%questItem = GuiQuestTasksWnd-->questsListCtrl.getObject(%i);
		questTasks_setQuestActive(%questItem.questId);
	}
}
//-----------------------------------------------------------------------------
function questTask_setFirstQuestActive()
{
	if (!isObject(GuiQuestTasksWnd))
	{
		echo("Quest tasks window not found");
		return;
	}

	%itemsCount = GuiQuestTasksWnd-->questsListCtrl.getCount();
	if (%itemsCount > 0)
	{
		%questItem = GuiQuestTasksWnd-->questsListCtrl.getObject(0);
		questTasks_setQuestActive(%questItem.questId);
	}
}
//-----------------------------------------------------------------------------
function GuiTaskOnMapButton::toggle(%this, %questId, %taskId)
{
	%isOn = %this.getStateOn();
	SetActiveTask(%questId, %taskId, %isOn);
}
//-----------------------------------------------------------------------------
function questTasks_TaskOnMapButtonToggleFromCode(%questId, %taskId, %isOn)
{
	%itemsCount = GuiQuestTasksWnd-->questsListCtrl.getCount();

	for (%i = 0; %i < %itemsCount; %i++)
	{
		%questItem = GuiQuestTasksWnd-->questsListCtrl.getObject(%i);
		if (%questItem.questId == %questId)
		{
			%tasksCount = %questItem-->questTasks.getCount();
			for (%t = 0; %t < %tasksCount; %t++)
			{
				%taskItem = %questItem-->questTasks.getObject(%t);
				if (%taskItem.taskId == %taskId)
				{
					%taskItem-->taskOnMapBtn.setStateOn(%isOn);
				}
			}
		}
	}
}
//-----------------------------------------------------------------------------
function questTask_ShowQuestTaskTutorialHint(%questId, %taskId)
{
	echo("show tutorial hint");
}
//-----------------------------------------------------------------------------
function questTask_ShowQuestTaskDescription(%description)
{
	if (!isObject(GuiQuestTasksDescriptionWnd))
	{
		new GuiWindowCtrl(GuiQuestTasksDescriptionWnd)
		{
			text = GetMessageIDText(4415);
			resizeWidth = "1";
			resizeHeight = "1";
			canMove = "1";
			canClose = "1";
			canMinimize = "0";
			canMaximize = "0";
			canCollapse = "0";
			canHideOnFreelook = "0";
			canLock = "0";
			canSetup = "0";
			edgeSnap = "1";
			margin = "0 0 0 0";
			padding = "0 0 0 0";
			anchorTop = "1";
			anchorBottom = "0";
			anchorLeft = "1";
			anchorRight = "0";
			position = "0 0";
			extent = "500 300";
			minExtent = "500 300";
			profile = "GuiWindowProfile";
			visible = "1";
			active = "1";
			isContainer = "1";
			closeCommand ="closeQuestTaskDescriptionWindow();";

			// client area
			new GuiContainer()
			{
				docking = Client;
				position = "0 0"; //"8 43";
				extent = "100 50";
				canSaveDynamicFields = "0";
				enabled = "1";
				profile = "QuestTasksContentProfile";
				canSave = "1";
				visible = "1";
				canHit = "true";

				// background
				new GuiBitmapCtrl()
				{
					position = "0 0";
					extent = "100% 100%";
					profile = "QuestTasksAtlas3Profile";
					visible = "1";
					canHit = "false"; 
					imageIndex = "LearningWindowBackground";
				};


				new GuiScrollCtrl()
				{
					hScrollBar = "alwaysOff";
					profile = "GuiEagleScrollBarProfile";
					constantThumbHeight = true;
					childMargin = "0 7";
					trackOffset = 8;
					lockHorizScroll = true;
					position = "25 12";
					extent = "75 26";
					isContainer = "1";
					horizSizing = "width";
					vertSizing = "height";

					new GuiMLTextCtrl()
					{
						text = "";
						position = "0 0";
						extent = "80 1";
						profile = "QuestTaskDescriptionTextProfile";
						canHit = "false"; 
						visible = "1";
						horizSizing = "width";
						bottomOffset = "0";
						internalName = "descriptionTxt";
					};
				};
			};
		};

		GuiQuestTasksDescriptionWnd.position.x = (PlayGui.extent.x - GuiQuestTasksDescriptionWnd.extent.x) / 2;
		GuiQuestTasksDescriptionWnd.position.y = (PlayGui.extent.y - GuiQuestTasksDescriptionWnd.extent.y) / 2;
		PlayGui.add(GuiQuestTasksDescriptionWnd);
	}


	GuiQuestTasksDescriptionWnd-->descriptionTxt.setText(GetMessageIDText(%description));
}
//-----------------------------------------------------------------------------
function closeQuestTaskDescriptionWindow()
{
	if (isObject(GuiQuestTasksDescriptionWnd))
	{
		PlayGui.remove(GuiQuestTasksDescriptionWnd);
		GuiQuestTasksDescriptionWnd.safeDeleteObject();
	}
}
//-----------------------------------------------------------------------------
function QuestTasksRootButtonCtrl::onMouseOverChanged(%this, %mouse_over)
{
	%this-->MouseOverPnl.setVisible(%mouse_over);
	%this-->questName.isMouseOver = %mouse_over;

	questTasks_setQuestItemName(%this-->questName, %this.isActiveQuest);
}
//-----------------------------------------------------------------------------
function GuiQuestTasksWnd::onQuestClicked(%this, %questId)
{
	questTask_OnQuestClicked(%questId);
	$pref::Quests::lastActiveQuest = %questId;
	%this.setActiveQuest(%questId);
}
//-----------------------------------------------------------------------------
function GuiQuestTasksWnd::setActiveQuest(%this, %questId)
{
	%itemsCount = %this-->questsListCtrl.getCount();

	for (%i = 0; %i < %itemsCount; %i++)
	{
		%questItem = %this-->questsListCtrl.getObject(%i);

		if (%questItem.questId == %questId)
		{
			if (%questItem-->questCaptionCtrl.isActiveQuest)
			{
				%this.setQuestItemActiveState(%questItem, false);
			}
			else
			{
				%this.setQuestItemActiveState(%questItem, true);
			}
		}
		else
		{
			%this.setQuestItemActiveState(%questItem, false);
		}
	}
}
//-----------------------------------------------------------------------------
function GuiQuestTasksWnd::setQuestTaskStatus(%this, %questId, %taskId, %taskStatus)
{
	%itemsCount = %this-->questsListCtrl.getCount();

	for (%i = 0; %i < %itemsCount; %i++)
	{
		%questItem = %this-->questsListCtrl.getObject(%i);

		if (%questItem.questId == %questId)
		{
			%taskCount = %questItem-->questTasks.getCount();

			for (%j = 0; %j < %taskCount; %j++)
			{
				%taskItem = %questItem-->questTasks.getObject(%j);

				if (%taskItem.taskId == %taskId)
				{
					%taskItem.taskStatus = %taskStatus;
					questTasks_updateQuestTaskProps(%taskItem);
					return;
				}
			}
		}
	}

}
//-----------------------------------------------------------------------------
function GuiQuestTasksWnd::setQuestItemActiveState(%this, %questItem, %isActive)
{
	if (!isObject(%questItem))
	{
		return;
	}

	%questItem-->questCaptionCtrl.isActiveQuest = %isActive;
	%questItem-->questIcon.visible = %isActive;
	%questItem-->questTasks.visible = %isActive;
	%questItem-->questTasksStub.visible = %isActive;

	// if (%isActive)
	// {
		// %questItem-->questCaptionCtrl.extent.y = 43;
		// %questItem-->MouseOverPnl.position.y = 8;
		// %questItem-->questName.position.y = 10;
	// }
	// else
	// {
		// %questItem-->questCaptionCtrl.extent.y = 26;
		// %questItem-->MouseOverPnl.position.y = 0;
		// %questItem-->questName.position.y = 2;
	// }

	questTasks_setQuestItemName(%questItem-->questName, %isActive);
}
//-----------------------------------------------------------------------------
$Quests::TaskWindow::QuestNameFontSize = 22;
$Quests::TaskWindow::TaskNameFontSize = 19;
//-----------------------------------------------------------------------------
function getActiveQuestNameText(%textId)
{
	if (!isObject(GuiQuestTasksWnd))
	{
		return GetMessageIDText(%textId);
	}
	return "<spush><font:" @ $GlobalCaptionFontName @ ":" @ $Quests::TaskWindow::QuestNameFontSize @ "><color:b2801c><clip:" @ GuiQuestTasksWnd-->questsListCtrl.extent.x @ ">" @ GetMessageIDText(%textId) @ "</clip><spop>";
}
//-----------------------------------------------------------------------------
function getInactiveQuestNameText(%textId)
{
	if (!isObject(GuiQuestTasksWnd))
	{
		return GetMessageIDText(%textId);
	}
	return "<spush><font:" @ $GlobalCaptionFontName @ ":" @ $Quests::TaskWindow::QuestNameFontSize @ "><color:999999><clip:" @ GuiQuestTasksWnd-->questsListCtrl.extent.x @ ">" @ GetMessageIDText(%textId) @ "</clip><spop>";
}
//-----------------------------------------------------------------------------
function getHighlightedQuestNameText(%textId)
{
	if (!isObject(GuiQuestTasksWnd))
	{
		return GetMessageIDText(%textId);
	}
	return "<spush><font:" @ $GlobalCaptionFontName @ ":" @ $Quests::TaskWindow::QuestNameFontSize @ "><color:FFFFFF><clip:" @ GuiQuestTasksWnd-->questsListCtrl.extent.x @ ">" @ GetMessageIDText(%textId) @ "</clip><spop>";
}
//-----------------------------------------------------------------------------
function getCompletedQuestTaskNameText(%textId, %textWidth)
{
	if (!isObject(GuiQuestTasksWnd))
	{
		return GetMessageIDText(%textId);
	}
	return "<spush><font:" @ $GlobalTextFontName @ ":" @ $Quests::TaskWindow::TaskNameFontSize @ "><color:444444><clip:" @ %textWidth @ ">" @ GetMessageIDText(%textId) @ "</clip><spop>";
}
//-----------------------------------------------------------------------------
function getIncompletedQuestTaskNameText(%textId, %textWidth)
{
	if (!isObject(GuiQuestTasksWnd))
	{
		return GetMessageIDText(%textId);
	}
	return "<spush><font:" @ $GlobalTextFontName @ ":" @ $Quests::TaskWindow::TaskNameFontSize @ "><color:777777><clip:" @ %textWidth @ ">" @ GetMessageIDText(%textId) @ "</clip><spop>";
}
//-----------------------------------------------------------------------------
function getCurrentQuestTaskNameText(%textId, %textWidth)
{
	if (!isObject(GuiQuestTasksWnd))
	{
		return GetMessageIDText(%textId);
	}
	return "<spush><font:" @ $GlobalTextFontName @ ":" @ $Quests::TaskWindow::TaskNameFontSize @ "><color:cccccc><clip:" @ %textWidth @ ">" @ GetMessageIDText(%textId) @ "</clip><spop>";
}
//-----------------------------------------------------------------------------
function questTasks_setQuestItemName(%questNameCtrl, %isActiveQuest)
{
	if (%questNameCtrl.isMouseOver)
	{
		if (%isActive)
		{
			%questNameCtrl.setText(getActiveQuestNameText(%questNameCtrl.originalQuestName));
		}
		else
		{
			%questNameCtrl.setText(getHighlightedQuestNameText(%questNameCtrl.originalQuestName));
		}
	}
	else
	{
		if (%isActiveQuest)
		{
			%questNameCtrl.setText(getActiveQuestNameText(%questNameCtrl.originalQuestName));
		}
		else
		{
			%questNameCtrl.setText(getInactiveQuestNameText(%questNameCtrl.originalQuestName));
		}
	}
}
//-----------------------------------------------------------------------------
function questTasks_updateQuestTaskProps(%taskItem)
{
	%textWidth = %taskItem-->taskInfoBtn.position.x - %taskItem-->taskName.position.x;
	if (%textWidth < 0) %textWidth = 0;

	if (%taskItem.taskStatus == 0)
	{
		// таск не выполнен
		%taskItem-->taskName.setText(getCurrentQuestTaskNameText(%taskItem.taskName, %textWidth));
		%taskItem-->taskInfoBtn.visible = true;
		%taskItem-->taskOnMapBtn.visible = %taskItem.hasMapButton;
		%taskItem-->taskBullet.imageIndex = "TaskCheckBoxOn";
		%taskItem-->taskTutorialHintBtn.visible = %taskItem.hasTutorialHint;
	}
	else if (%taskItem.taskStatus == 1)
	{
		// таск выполнен
		%taskItem-->taskName.setText(getCompletedQuestTaskNameText(%taskItem.taskName, %textWidth));
		%taskItem-->taskInfoBtn.visible = false;
		%taskItem-->taskOnMapBtn.visible = false;
		%taskItem-->taskBullet.imageIndex = "TaskCheckBoxDisabled";
		%taskItem-->taskTutorialHintBtn.visible = false;
	}
	else
	{
		echo("invalid task status");
	}

}
//-----------------------------------------------------------------------------
function QuestNameMLTextCtrl::onResize(%this, %width, %maxY)
{
	questTasks_setQuestItemName(%this, %this.getParent().isActiveQuest);
}
//-----------------------------------------------------------------------------
function QuestTaskNameMLTextCtrl::onResize(%this, %width, %maxY)
{
	questTasks_updateQuestTaskProps(%this.getParent());
}
//-----------------------------------------------------------------------------
function questTasks_debugAdd()
{
	createQuestTasksWindow();
	
	for(%i = 0; %i < 5; %i++)
	{
		questTask_addQuest(%i, %i + 1, "gui/images/quest/icons/hoytuhanir.png");
		questTasks_debugAddTask(%i);
	}
}
//-----------------------------------------------------------------------------
function questTasks_debugAddTask(%questId)
{
	for(%i = 0; %i < 5; %i++)
	{
		questTask_addQuestTask(%questId, %i, %i + 1, %i % 2, %i + 1, (%i + 1) % 3, (%i + 1) % 3);
	}
}
//-----------------------------------------------------------------------------
