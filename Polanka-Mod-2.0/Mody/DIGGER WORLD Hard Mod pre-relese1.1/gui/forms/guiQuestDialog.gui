//-----------------------------------------------------------------------------
function createQuestDialogWindow()
{
	createQuestDialogWindowProfiles();

	if (isObject(GuiQuestDialogWindow))
	{
		return GuiQuestDialogWindow;
	}

	%questWnd = new GuiWindowCtrl(GuiQuestDialogWindow)
	{
		text = GetMessageIDText(4414);
		resizeWidth = "1";
		resizeHeight = "1";
		canMove = "1";
		canClose = "1";
		canMinimize = "0";
		canMaximize = "0";
		canCollapse = "0";
		canHideOnFreelook = "0";
		canLock = "0";
		canSetup = "0";
		edgeSnap = "1";
		margin = "0 0 0 0";
		padding = "0 0 0 0";
		anchorTop = "1";
		anchorBottom = "0";
		anchorLeft = "1";
		anchorRight = "0";
		position = "0 0";
		extent = "570 700";
		minExtent = "500 500";
		profile = "GuiAutoCursorWindowProfile";
		visible = "1";
		active = "1";
		isContainer = "1";
		closeCommand ="closeQuestDialogWindow();";
		autoClose = "1";

		// client area
		new GuiContainer()
		{
			docking = Client;
			position = "0 0"; //"8 43";
			extent = "550 472";
			canSaveDynamicFields = "0";
			enabled = "1";
			profile = "QuestDialogContentProfile";
			canSave = "1";
			visible = "1";
			canHit = "true";

			// background
			new GuiBitmapCtrl()
			{
				position = "0 0";
				extent = "100% 100%";
				profile = "QuestDialogAtlas3Profile";
				visible = "1";
				canHit = "false"; 
				imageIndex = "LearningWindowBackground";
			};

			// орнамент
			new GuiBitmapCtrl()
			{
				position = "0 0";
				extent = "156 472";
				profile = "QuestDialogContentProfile";
				visible = "1";
				canHit = "false"; 
				imageIndex = "LearningWindowBackground";
				bitmap = "gui/images/quest/ornament_dailog.png";
				wrap = "0";
				resize = "1";
				centered = "1";
				vertSizing = "top";
			};

			// top part (quest name and icon)
			new GuiContainer()
			{
				docking = Top;
				position = "0 0";
				extent = "100 73";
				enabled = "1";
				profile = "QuestDialogContentProfile";
				visible = "1";
				canHit = "false";

				// иконка
				new GuiBitmapCtrl()
				{
					position = "35 11";
					extent = "50 50";
					profile = "QuestDialogContentProfile";
					visible = "1";
					active = "1";
					centered = "1";
					canHit = "false";
					wrap = "0";
					resize = "1";
					bitmap = "logs/icon_dialog.png";
					internalName = "questGiverIcon";
				};

				// имя
				new GuiMLTextCtrl()
				{
					text = "";
					position = "85 22";
					extent = "14 1";
					profile = "QuestDialogContentProfile";
					canHit = "false"; 
					visible = "1";
					horizSizing = "width";
					internalName = "questGiverName";
					bottomOffset = "0";
				};

				// bottom border
				new GuiContainer()
				{
					docking = Bottom;
					position = "0 0";
					extent = "100 3";
					minExtent = "8 2";
					enabled = "1";
					profile = "QuestDialogContentProfile";
					visible = "1";
					canHit = "false";

					new GuiControl()
					{
						position = "11 0";
						extent = "78 3";
						profile = "QuestDialogBorderProfile";
						horizSizing = "width";
						LeftIndex0 = "BlackSlimHLineLeft";
						TopIndex0 = "BlackSlimHLineCenter";
						RightIndex0 = "BlackSlimHLineRight";
					};
				};
			};

			// bottom part - answers
			new GuiContainer()
			{
				docking = Bottom;
				position = "0 0";
				extent = "500 100";
				enabled = "1";
				profile = "QuestDialogContentProfile";
				visible = "1";
				internalName = "answersContainer";

				// top border
				new GuiContainer()
				{
					docking = top;
					position = "0 0";
					extent = "100 3";
					minExtent = "8 2";
					enabled = "1";
					profile = "QuestDialogContentProfile";
					visible = "1";
					canHit = "false";

					new GuiControl()
					{
						position = "11 0";
						extent = "78 3";
						profile = "QuestDialogBorderProfile";
						horizSizing = "width";
						LeftIndex0 = "BlackSlimHLineLeft";
						TopIndex0 = "BlackSlimHLineCenter";
						RightIndex0 = "BlackSlimHLineRight";
					};
				};

				new GuiMLTextCtrl()
				{
					text = "";
					position = "36 35";
					extent = "500 1";
					profile = "QuestDialogContentProfile";
					canHit = "false"; 
					visible = "1";
					horizSizing = "width";
					bottomOffset = "0";
					internalName = "answersHeader";
				};

				new GuiStackControl()
				{
					position = "0 76";
					extent = "460 0";
					minExtent = "1 1";
					horizSizing = "width";
					Stacking = Vertical;
					profile = "QuestDialogContentProfile";
					vertStacking = "Top to Bottom";
					padding = 0;
					dynamicSize = "1";
					dynamicNonStackExtent = "0";
					changeChildPosition = "1";
					changeChildSizeToFit = "1";
					visible = "1";
					internalName = "questAnswers";
				};
			};

			// middle part - quest text
			new GuiContainer()
			{
				docking = Client;
				position = "0 0";
				extent = "100 100";
				enabled = "1";
				profile = "QuestDialogContentProfile";
				visible = "1";
				canHit = "true";

				new GuiScrollCtrl()
				{
					hScrollBar = "alwaysOff";
					profile = "GuiEagleScrollBarProfile";
					constantThumbHeight = true;
					childMargin = "0 7";
					trackOffset = 8;
					lockHorizScroll = true;
					position = "40 29";
					extent = "60 60";
					isContainer = "1";
					horizSizing = "width";
					vertSizing = "height";

					new GuiMLTextCtrl()
					{
						text = "";
						position = "0 0";
						extent = "58 1";
						profile = "QuestDialogMainTextProfile";
						canHit = "false"; 
						visible = "1";
						horizSizing = "width";
						internalName = "questGiverText";
						bottomOffset = "0";
					};
				};

			};
		};
	};

	%questWnd.position.x = (PlayGui.extent.x - %questWnd.extent.x) / 2;
	%questWnd.position.y = (PlayGui.extent.y - %questWnd.extent.y) / 2;

	PlayGui.add(%questWnd);
	return %questWnd;
}
//-----------------------------------------------------------------------------
function closeQuestDialogWindow()
{
	if (isObject(GuiQuestDialogWindow))
	{
		PlayGui.remove(GuiQuestDialogWindow);
		GuiQuestDialogWindow.safeDeleteObject();
	}
}
//-----------------------------------------------------------------------------
function createQuestDialogWindowProfiles()
{
	if (!isObject(QuestDialogContentProfile))
	{
		new GuiControlProfile(QuestDialogContentProfile: GuiBaseTextProfile)
		{
			// border = "rect1_color";
			// borderColor = "200 200 200";
		};
	}

	if (!isObject(QuestDialogAtlas3Profile))
	{
		new GuiControlProfile(QuestDialogAtlas3Profile: GuiBaseTextProfile)
		{
			globalImageIndex = "atlas3";
		};
	}

	if (!isObject(QuestDialogBorderProfile))
	{
		new GuiControlProfile(QuestDialogBorderProfile: GuiBaseTextProfile)
		{
			globalImageIndex = "atlas3";
			border = "all_tiling";
		};
	}

	if (!isObject(QuestDialogAnswerButtonProfile))
	{
		new GuiControlProfile(QuestDialogAnswerButtonProfile: QuestDialogAtlas3Profile)
		{
		};
	}

	if (!isObject(QuestDialogMainTextProfile))
	{
		new GuiControlProfile(QuestDialogMainTextProfile: GuiBaseTextProfile)
		{
			fontSize = 20;
			fontColor = "153 153 153";
		};
	}
}
//-----------------------------------------------------------------------------
function deleteQuestDialogWindowProfiles()
{
	if (isObject(QuestDialogContentProfile))
	{
		QuestDialogContentProfile.safeDeleteObject();
	}

	if (isObject(QuestDialogAtlas3Profile))
	{
		QuestDialogAtlas3Profile.safeDeleteObject();
	}

	if (isObject(QuestDialogBorderProfile))
	{
		QuestDialogBorderProfile.safeDeleteObject();
	}

	if (isObject(QuestDialogAnswerButtonProfile))
	{
		QuestDialogAnswerButtonProfile.safeDeleteObject();
	}
}
//-----------------------------------------------------------------------------
function setQuestDialogParameters(%questSubject, %questGiverName, %questGiverType, %questGiverAvatar, %prevQuestText, %currentQuestText, %answersHeaderMsgId)
{
	if (!isObject(GuiQuestDialogWindow))
	{
		echo("GuiQuestDialogWindow not found");
		return;
	}
	GuiQuestDialogWindow.questSubject = %questSubject;
	GuiQuestDialogWindow-->questGiverIcon.bitmap = %questGiverAvatar;
	GuiQuestDialogWindow-->questGiverName.setText(getQuestGiverNameText(%questGiverName));
	GuiQuestDialogWindow-->questGiverText.setText(GetMessageIDText(%currentQuestText));

	%msgId = %answersHeaderMsgId;

	GuiQuestDialogWindow-->answersHeader.setText("<spush><font:" @ $GlobalCaptionFontName @ ":23><color:c38100>" @ GetMessageIDText(%msgId) @ "<spop>");
	GuiQuestDialogWindow.prevQuestText = %prevQuestText;
}
//-----------------------------------------------------------------------------
function addQuestDialogAnswer(%answerId, %textId, %answerType)
{
	if (!isObject(GuiQuestDialogWindow))
	{
		echo("GuiQuestDialogWindow not found");
		return;
	}

	%answer = new GuiButtonCtrl()
	{
		text = "";
		groupNum = "-1";
		buttonType = "PushButton";
		position = "0 0";
		extent = "150 29";
		minExtent = "8 2";
		profile = "QuestDialogAnswerButtonProfile";
		visible = "1";
		active = "1";
		command = "GuiQuestDialogWindow.onAnswerClicked(" @  %answerId @ ");";
		horizSizing = "width";
		isContainer = "1";
		class = "QuestDialogAnswerButtonCtrl";

		// bullet
		new GuiBitmapCtrl()
		{
			position = "36 6";
			extent = "16 16";
			profile = "GuiAtlas3ImageProfile";
			visible = "1";
			imageIndex = "TaskCheckBoxDisabled";
		};

		// иконка рядом с ответом
		new GuiBitmapCtrl()
		{
			position = "36 0";
			extent = "29 29";
			profile = "GuiAtlas3ImageProfile";
			visible = "1";
			active = "1";
			centered = "1";
			canHit = "false";
			wrap = "0";
			resize = "1";
			internalName = "answerIcon";
			imageIndex = getQuestAnswerTypeIcon(%answerType);
		};

		new GuiMLTextCtrl()
		{
			text = getQuestAnswerNormalText(%textId);
			position = "69 4";
			extent = "80 1";
			profile = "QuestDialogContentProfile";
			canHit = "false"; 
			visible = "1";
			horizSizing = "width";
			internalName = "answerText";
			bottomOffset = "0";
			originalQuestAnswer = %textId;
			class = "QuestDialogAnswerMLTextCtrl";
		};
	};

	GuiQuestDialogWindow-->questAnswers.add(%answer);
}
//-----------------------------------------------------------------------------
function clearQuestDialogAnswers()
{
	if (!isObject(GuiQuestDialogWindow))
	{
		echo("GuiQuestDialogWindow not found");
		return;
	}

	GuiQuestDialogWindow-->questAnswers.clear();
}
//-----------------------------------------------------------------------------
function GuiQuestDialogWindow::onAnswerClicked(%this, %answerId)
{
	QuestAnswerPressed(%this.questSubject, %answerId);
}
//-----------------------------------------------------------------------------
function QuestDialogAnswerButtonCtrl::onMouseOverChanged(%this, %mouse_over)
{
	if (%mouse_over)
	{
		%this-->answerText.setText(getQuestAnswerHighlightText(%this-->answerText.originalQuestAnswer));
	}
	else
	{
		%this-->answerText.setText(getQuestAnswerNormalText(%this-->answerText.originalQuestAnswer));
	}
}
//-----------------------------------------------------------------------------
function getQuestGiverNameText(%textId)
{
	return "<spush><font:" @ $GlobalCaptionFontName @ ":27><color:b2801c>" @ GetMessageIDText(%textId) @ "<spop>";
}
//-----------------------------------------------------------------------------
function getQuestAnswerNormalText(%textId)
{
	return "<spush><font:" @ $GlobalTextFontName @ ":20><color:c38100>" @ GetMessageIDText(%textId) @ "<spop>";
}
//-----------------------------------------------------------------------------
function getQuestAnswerHighlightText(%textId)
{
	return "<spush><font:" @ $GlobalTextFontName @ ":20><color:d2a100>" @ GetMessageIDText(%textId) @ "<spop>";
}
//-----------------------------------------------------------------------------
function QuestDialogAnswerMLTextCtrl::onResize(%this, %width, %maxY)
{
	%thisParent = %this.getParent();
	%thisParent.extent.y = %this.extent.y + 4 + 4;
	%thisParent-->answerIcon.position.y = (%thisParent.extent.y - %thisParent-->answerIcon.extent.y) / 2;

	if (isObject(GuiQuestDialogWindow))
	{
		GuiQuestDialogWindow-->answersContainer.extent.y = GuiQuestDialogWindow-->questAnswers.position.y + GuiQuestDialogWindow-->questAnswers.extent.y + 50;
	}
}
//-----------------------------------------------------------------------------
function getQuestAnswerTypeIcon(%answerType)
{
	if (%answerType == 1)   // Return
	{
		return "ReturnIconNormal";
	}
	else if (%answerType == 2)   // Attention
	{
		return "ExclamationPointIconNormal";
	}
	else if (%answerType == 3)   // Close
	{
		return "XIconNormal";
	}
	else
	{
		return "";
	}
}