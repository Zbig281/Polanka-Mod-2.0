//-----------------------------------------------------------------------------
function createCombosWindow()
{
	new CmCombosWindow(CombosBar) 
	{
		position = "49.5313% 21.5278%";
		MinExtent = "310 74";
		MaxExtent = "500 111";
		Extent = "500 111";
		canSave = "1";
		Visible = "0";
		text = GetMessageIDText(1303); // Combos
		canSetup = "1";
		canMinimize = "0";
		canMaximize = "0";
		canClose = "0";
		resizeWidth = "0";
		//resizeHeight = "0";
		canHideOnFreelook = "1";
		canLock = "1";
		locked = "1";
		forGuiEdit = "1";
		isContainer = "1";
		profile = "GuiWindowProfile";
		stackMinSize = 0;
		
		new GuiStackControl()
		{
			position = "9 29";
			extent = "10 10";
			Visible = "1";
			profile = "GuiDefaultProfile";
			internalName = "comboStackCtrl";
			stackingType = "Horizontal";
			horizStacking = "Left to Right";
			padding = 0;
			dynamicSize = "1";
			dynamicNonStackExtent = "1";
			changeChildPosition = "1";
			changeChildSizeToFit = "0";
			docking = "left";
		};
	};
	
	$minSize = 0;
	
	for($i = 0; $i < 4; $i++)
	{
		%comboStep = createComboStep();
		%comboStep.internalName = "comboArrow" @ $i;
		CombosBar-->comboStackCtrl.add(%comboStep);
		$minSize += %comboStep.extent.x;
	}
	
	%comboText = new GuiTextCtrl()
	{
		text = "";
		extent = "100 73";
		profile = "ComboWindowTextProfile";
		canHit = "false"; 
		visible = "1";
		internalName = "comboFinalText";
		useParentOpacity = "0";
	};
	
	CombosBar-->comboStackCtrl.add(%comboText);
	CombosBar.stackMinSize = $minSize + 12;
	
	PlayGui.add(CombosBar);
}
//-----------------------------------------------------------------------------
function closeCombosWindow()
{
	if (isObject(CombosBar))
	{
		CombosBar.visible = "0";
	}
}
//-----------------------------------------------------------------------------
function deleteCombosWindow()
{
	deleteCombosWindowProfiles();
	if (isObject(CombosBar))
	{
		CombosBar.safeDeleteObject();
	}
}
//-----------------------------------------------------------------------------
/*
function createCombosWindowProfiles()
{
}
function deleteCombosWindowProfiles()
{
}
*/
//-----------------------------------------------------------------------------
function createComboStep()
{
	%guiComboStep = new guiComboStep()
	{
		position = "0 0";
		extent = "73 73";
		minExtent = "8 2";
		profile = "GuiBaseTextProfile";
		visible = "1";
		active = "1";
		isContainer = "1";
		
		new ButtonCooldown()
		{
			position = "0 0";
			extent = "73 73";
			minExtent = "8 2";
			profile = "GuiComboImageProfile";
			visible = "1";
			active = "1";
			isContainer = "0";
			isCoolUp = "0";
			canHit = "false"; 
			startGlobalImageIndex = getComboWindowStartCooldown();
			endGlobalImageIndex = getComboWindowEndCooldown();
			internalName = "comboCooldown";
		};
		
		new GuiBitmapCtrl()
		{
			position = "0 0";
			extent = "73 73";
			profile = "GuiComboImageProfile";
			visible = "1";
			active = "1";
			centered = "1";
			canHit = "false"; 
			internalName = "comboHit";
			wrap = "0";
		};
	};
	
	return %guiComboStep;
}
//-----------------------------------------------------------------------------
function CmCombosWindow::onChildControlResized(%this)
{
	if (!isObject(CombosBar))
		return;
		
	%comboStackCtrl = CombosBar.findObjectByInternalName("comboStackCtrl", true);
	if (!isObject(%comboStackCtrl))
		return;
		
	$extX = getMax(%comboStackCtrl.extent.x + 12, CombosBar.stackMinSize); // +12 - for border
	
	CombosBar.MaxExtent.x = $extX;
	CombosBar.minExtent.x = $extX;
	CombosBar.extent.x = $extX;
}
//-----------------------------------------------------------------------------
function CmCombosWindow::updateControls(%this, %bMaximized)
{
	if (!isObject(CombosBar))
		return;
		
	$iconSize = 0;
	$finalTextProfile = "";
	
	if (%bMaximized)
	{
		$iconSize = 73;
		$finalTextProfile = "ComboWindowTextProfile";
	}
	else
	{
		$iconSize = 36;
		$finalTextProfile = "ComboWindowSmallTextProfile";
	}
	
	CombosBar.stackMinSize = $iconSize * 4 + 12;

	for($i = 0; $i < 4; $i++)
	{
		%comboStep = CombosBar.findObjectByInternalName("comboArrow" @ $i, true);
		%comboStep.extent = $iconSize @ " " @ $iconSize;
		updateControlsComboStep(%comboStep, %bMaximized);
	}
	
	%comboFinalText = CombosBar.findObjectByInternalName("comboFinalText", true);
	%comboFinalText.profile = $finalTextProfile;
	%comboFinalText.extent.y = $iconSize;
}
//-----------------------------------------------------------------------------
function updateControlsComboStep(%comboStep, %bMaximized)
{
	%comboCooldown = %comboStep.findObjectByInternalName("comboCooldown", true);
	%comboHit = %comboStep.findObjectByInternalName("comboHit", true);
	
	if (%bMaximized)
	{
		%comboCooldown.extent = "73 73";
		%comboHit.extent = "73 73";
		%comboHit.position = "0 0";
	}
	else
	{
		%comboCooldown.extent = "36 36";
		%comboHit.extent = "25 25";
		%comboHit.position.x = %comboCooldown.extent.x / 2 - %comboHit.extent.x / 2;
		%comboHit.position.y = %comboCooldown.extent.y / 2 - %comboHit.extent.y / 2;
	}
}
//-----------------------------------------------------------------------------
