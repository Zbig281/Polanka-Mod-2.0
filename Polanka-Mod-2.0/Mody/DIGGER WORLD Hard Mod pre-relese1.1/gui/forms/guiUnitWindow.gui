//-----------------------------------------------------------------------------
// Craftsman & Marksman: Life is feudal
//-----------------------------------------------------------------------------

singleton GuiControlProfile(UnitDefaultProfile : GuiBaseCaptionProfile)
{
	fontSize = 20;
	fontColor = "202 202 202";
	textOffset = "0 0";
};

singleton GuiControlProfile(UnitSmallFontProfile : UnitDefaultProfile)
{
	fontSize = 14;
};

function createUnitWindow()
{
	new GuiUnitWindow(GroupUnitWindow) 
	{
		isContainer = "1";
		position = "800 100";
		Extent = "200 40";
		MinExtent = "200 40";
		canSave = "1";
		Visible = "1";
		text = GetMessageIDText(1304); // Group
		Profile = "GuiSmallWindowProfile";
		canSaveDynamicFields = "0";
		closeCommand ="closeGroupUnitWindow();";
		resizeWidth = "1";
		resizeHeight = "0";
		canMove = "1";
		canClose = "0";
		canMaximize = "0";
		canMinimize = "0";
		canHideOnFreelook = "1";
		canLock = "1";
		canSetup = "1";
		forGuiEdit = "1";
		locked = "1";
		active = "1";
		
		new GuiStackControl()
		{
			position = "9 29";
			extent = "182 10";
			profile = "GuiDefaultProfile";
			internalName = "unitWidgetStackCtrl";
			stackingType = Vertical;
			padding = 8;
			dynamicSize = "1";
			dynamicNonStackExtent = "0";
			changeChildPosition = "1";
			changeChildSizeToFit = "1";
		};
	};

	PlayGui.add(GroupUnitWindow);
}

//-----------------------------------------------------------------------------
function createUnitMemberWidget(%unitName, %unitIcon)
{
	if (!isObject(GroupUnitWindow))
		return NULL;

	%fontProfile = UnitDefaultProfile;
	if(strlen(%unitName) > 18)
		%fontProfile = UnitSmallFontProfile;
	
	%unitMemberWidget = new GuiMemberWidget()
	{
		position = "0 0";
		extent = "100% 70";
		minExtent = "8 2";
		profile = "GuiDefaultProfile";
		visible = "1";
		active = "1";
		isContainer = "1";
		canHit = "1";
		
		//background
		new GuiControl()
		{
			position = "0 0";
			extent = "100% 100%";
			profile = GuiAtlas3TiledImageProfile;
			visible = "1";
			active = "1";
			canHit = "0";
			BackgroundIndex = LearningWindowBackground;
		};

		//background border
		new GuiControl()
		{
			position = "0 0";
			extent = "100% 100%";
			profile = "GuiAtlas1TiledImageProfile";
			visible = "1";
			active = "1";
			canHit = "0";
			
			LeftIndex0 = FringeLeftShadow;
			RightIndex0 = FringeRightShadow;
			TopIndex0 = FringeTopShadow;
			BottomIndex0 = FringeBottomShadow;
			TopLeftIndex = FringeTLAngleShadow;
			TopRightIndex = FringeTRAngleShadow;
			BottomRightIndex = FringeBRAngleShadow;
			BottomLeftIndex = FringeBLAngleShadow;
		};
		
		//player icon
		new GuiBitmapCtrl()
		{
			position = "0 2";
			extent = "30 26";
			minExtent = "30 26";
			profile = "GuiAtlas3ImageProfile";
			visible = "1";
			active = "1";
			canHit = "0"; 
			wrap = "0";
			internalName = "barValue";
			imageIndex = %unitIcon;
		};

		//player name
		new GuiTextCtrl()
		{
			position = "32 5";
			extent = "95% 19";
			profile = %fontProfile;
			visible = "1";
			active = "1";
			text = %unitName;
			internalName = "unitName";
			canHit = "0";
		};
		
		new GuiStackControl()
		{
			position = "5 35";
			extent = "95% 10";
			profile = "GuiDefaultProfile";
			internalName = "unitBarStackCtrl";
			stackingType = Vertical;
			padding = 0;
			dynamicSize = "1";
			dynamicNonStackExtent = "0";
			changeChildPosition = "1";
			changeChildSizeToFit = "1";
			canHit = "0";
		};
	};

	%devideControl = new GuiControl()
		{
			position = "0 0";
			extent = "100% 3";
			minExtent = "1 3";
			profile = "GuiSkillStatImageProfile";
			visible = "1";
			active = "1";
			canHit = "0"; 
			wrap = "1";
		};
	
	%unitMemberWidget-->unitBarStackCtrl.add(createUnitMemberBar("unitSoftHPBar", SoftHPMiniBar));
	%unitMemberWidget-->unitBarStackCtrl.add(createUnitMemberBar("unitHardHPBar", HardHPMiniBar));
	%unitMemberWidget-->unitBarStackCtrl.add(%devideControl);
	%unitMemberWidget-->unitBarStackCtrl.add(createUnitMemberBar("unitSoftStaminaBar", SoftStamMiniBar));
	%unitMemberWidget-->unitBarStackCtrl.add(createUnitMemberBar("unitHardStaminaBar", HardStamMiniBar));
	
	GroupUnitWindow-->unitWidgetStackCtrl.add(%unitMemberWidget);
	GroupUnitWindow.extent.y = GroupUnitWindow-->unitWidgetStackCtrl.position.y + GroupUnitWindow-->unitWidgetStackCtrl.extent.y + 9;
	return %unitMemberWidget;
}
//-----------------------------------------------------------------------------
function createUnitMemberBar(%barName, %barImageIndex)
{
	%bar_ctrl = new GuiControl()//Control
	{
		position = "0 0";
		extent = "100 6";
		profile = "GuiDefaultProfile";
		visible = "1";
		active = "1";
		internalName = %barName;
		canHit = "0";
		minBarValue = "0";
		maxBarValue = "0";
		currentBarValue = "0";
		//imageIndex = "BackgroundBar";
		
		// background
		new GuiBitmapCtrl()
		{
			position = "0 0";
			extent = "100% 6";
			minExtent = "1 6";
			profile = "GuiSkillStatImageProfile";
			visible = "1";
			active = "1";
			canHit = "0"; 
			wrap = "1";
			internalName = "barBackground";
			imageIndex = AlignmentMiniBar;
		};

		// bar
		new GuiBitmapCtrl()
		{
			position = "0 0";
			extent = "1 6";
			minExtent = "1 6";
			profile = "GuiSkillStatImageProfile";
			visible = "1";
			active = "1";
			canHit = "0"; 
			wrap = "0";
			internalName = "barValue";
			imageIndex = %barImageIndex;
		};
	};
	
	return %bar_ctrl;
}
//-----------------------------------------------------------------------------
function clearUnitMemberWidgets()
{
	GroupUnitWindow-->unitWidgetStackCtrl.clear();
}
//-----------------------------------------------------------------------------
function closeGroupUnitWindow()
{
	if (isObject(GroupUnitWindow))
	{
		GroupUnitWindow.safeDeleteObject();
	}
}
//-----------------------------------------------------------------------------
function updateBarLayout(%barCtrl)
{
	if (!isObject(%barCtrl))
		return;
		
	%barExtent = %barCtrl.extent.x * (%barCtrl.currentBarValue - %barCtrl.minBarValue) / (%barCtrl.maxBarValue - %barCtrl.minBarValue);
	%barCtrl-->barValue.extent.x = %barExtent;
	//%barCtrl-->barValueBorder.position.x = %barExtent - 1;
}
//-----------------------------------------------------------------------------
function changeBarValues(%barCtrl, %val, %maxVal)
{
	if (!isObject(%barCtrl))
		return;
		
	%barCtrl.maxBarValue = %maxVal;
	%barCtrl.currentBarValue = mClamp(%val, %barCtrl.minBarValue, %barCtrl.maxBarValue);
	updateBarLayout(%barCtrl);
}
//-----------------------------------------------------------------------------
function GroupUnitWindow::onThisControlResized(%this)
{
	if (isObject(GroupUnitWindow))
	{
		GroupUnitWindow-->unitWidgetStackCtrl.position.x = 9;
		GroupUnitWindow-->unitWidgetStackCtrl.extent.x = GroupUnitWindow.extent.x - 18;
	}
}
//-----------------------------------------------------------------------------
function GuiMemberWidget::onChangeHardHP(%this, %val, %maxVal)
{
	%barCtrl = %this-->unitHardHPBar;
	changeBarValues(%barCtrl, %val, %maxVal);
}
//-----------------------------------------------------------------------------
function GuiMemberWidget::onChangeSoftHP(%this, %val, %maxVal)
{
	%barCtrl = %this-->unitSoftHPBar;
	changeBarValues(%barCtrl, %val, %maxVal);
}
//-----------------------------------------------------------------------------
function GuiMemberWidget::onChangeHardStam(%this, %val, %maxVal)
{
	%barCtrl = %this-->unitHardStaminaBar;
	changeBarValues(%barCtrl, %val, %maxVal);
}
//-----------------------------------------------------------------------------
function GuiMemberWidget::onChangeSoftStam(%this, %val, %maxVal)
{
	%barCtrl = %this-->unitSoftStaminaBar;
	changeBarValues(%barCtrl, %val, %maxVal);
}
//-----------------------------------------------------------------------------
